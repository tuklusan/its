
TS==0
IFE TS,RELOCA
MOBY==140000
IFN TS,MOBY==0
LPTR==1

IFE TS,TITLE TECO6
IFN TS,TITLE TSTECO

GLITCH==141
ALTMOD==175
EOFCHR==3

FF=0
P"=1
A"=2
AA=3
B=4
E=5
C=6
D=7
F=10
T=11
TT=12
TT1=13
IN=14
OUT=15
CH=16
PF=17
SYM=OUT

;RIGHT HALF FLAGS

ALTF==1
ARG2==2
ARG==4
ITERF==10
SLSL==20
PCHFLG==40
COLONF==100
SYLF==200
VIEWF==400
FINF==1000
FINDR==2000
QMFLG==4000
NOTF==10000
TRACEF==20000
LET==40000
PANIC==100000	;YANK GETTING NEAR END OF BUFFER
SLASHF==200000

;LEFT HALF FLAGS

NEGF==1	;DPT-ING A NEGATIVE NUMBER
LPTF==20
UREAD==200	;READING FROM UTAPE
UWRITE==400	;WRITING ON UTAPE
PNSTOP==1000	;STOP PUNCH
GLOF==2000	;CTRL O GLOB FLG
VARF==4000	;CTRL O VAR FLG
DEFF==10000	;CTRL O DEFINE FLAG
CTLUF==100000	;DO AN ^U AT END IF VIEWF OFF

IFE TS,[7R==20000
7W==40000
0BIT==1000	;PDP-7 LOSSAGE
1BIT==2000	;PDP-7 LOSSAGE
]
TYPR1=(30000)
TYPR2=(31000)
ITYPR=(32000)	
IITYPR=(33000)
IFE TS,[MACDMP==37400+MOBY
MACCR==37777+MOBY
DDT==34000+MOBY
IFE MOBY,LPDL==75
IFN MOBY,LPDL==200
]
IFN TS,LPDL=140
GCTBL==100
LPF==100


IFN TS,[
;TS SYMBOLS

IOT=40_27.
STOPEN=41_27.
DEFINE TSOPEN A,B
	STOPEN A,B
	JSP TT,OPNER
	TERMIN
OPER=42_27.
CALL=43_27.
USET=44_27.
STATUS=46_27.

DISMISS=CALL 1,
TSVALRET=CALL 4,
CORE=CALL 6,
DSTART=CALL 10,
FDELE=CALL 11,

ITYI=OPER 1
SLEEP=OPER 3
SETMSK=OPER 4
TSCLOSE=OPER 7
DFLUSH=OPER 15
DSTOP=OPER 16
RDTIM=OPER 17
RDSW=OPER 20
UFLAP=OPER 22
RD760=OPER 30
WR760=OPER 31
RSNAME=OPER 34
WSNAME=OPER 35
RESET=OPER 37
.ASSIGN=OPER 43
.DESIGN=OPER 44
RTIME=OPER 45
RDATE=OPER 46
.EOFC=OPER 50
.GPICLR=OPER 60

SYSGE==2

PDLOV1==200000
MPV1==20000
IOCERR==400
DISMPV==20
TYPIN==1
TSMSK==PDLOV1+DISMPV+MPV1+IOCERR+TYPIN

TYIC==1
TYOC==2
UTYIC==3
UTYOC==4
LPTC==5
FDRC==6	;FOR READING FILE DIRECTORIES
DCHN==7
]
INIT":
LOC 41
JSR ETYPER

IFN TS,[LOC 42
	JSR TSINT
]
LOC INIT


IFE TS,	CONO 633550+APRCHN
	MOVE P,[(,-LPDL)PDL-1]
IFE TS,[CONO PTR,20
	CONO PTR,0
	CONO PI,12237
]	SETOM CTLBF'
	MOVEI A,200*5
	MOVEM A,BEG
	MOVEM A,PT
	MOVEM A,Z
	MOVEM A,QRBUF
	MOVEI A,CBUF"+77
	MOVEM A,CBUFH
	CLEARB FF,SFINDF
GOZ":IFE TS,[SETOM .COAST"
	CLEARM 7ST
	CONO 760,0
	CONI 760,A
	ANDI A,0BIT+1BIT
	MOVEM A,7STA
	CONO PI,2237
	SETOM .LSTP"
]	MOVE P,[(,-LPDL)PDL-1]
	MOVE PF,[(,-LPF)PFL-1]
IFN TS,[MOVEI B,TSMSK
	SETMSK B,
	TSOPEN TYOC,OTTY
	TSOPEN TYIC,ITTY
	STATUS TYOC,B
	ANDI B,77
	SETZM GETTY'
	CAIE B,SYSGE
	JRST GOZ2
	SETOM GETTY	;GE CONSOLE HACKS
	SETOM RGETTY'
	MOVEI B,9	;CONSERVATIVE TO AVOID AUTO CR SCREW
	MOVEM B,NLINES
	MOVEI B,5
	HRLM B,OTTY
GOZ2:	TSOPEN DCHN,OTTY
	IITYPR [SIXBIT /TECO./]
	IITYPR [.FNAM2]
]

GOX:IFE TS,SETOM FSHFLG
	CLEARM STOPF
IFE TS,[CLEARM BRKFLG
	CONO TTY,10+TTYCHN
	SKIPG TICC
	JRST .+3
	PUSHJ P,TYI
	JRST .-3
	CLEARM FSHFLG
]	PUSHJ P,CRR

GO:	MOVE P,[-LPDL,,PDL-1]
IFE TS,	SKIPL FSHFLG
	SKIPGE STOPF
	JRST GOX
IFN TS,	PUSHJ P,CORLES
	TRZN FF,VIEWF
	JRST VIEW2
IFE TS,	CONO PI,4002
IFE TS,	CONO 433550+APRCHN
GO1:	CLEARM,LEV
	TRZ FF,777777#QMFLG
	JRST LIS

IFE TS,[
TYI:	ILDB CH,MACCR
	JUMPN CH,CPOPJ
	SETZM MACCR
	MOVEI CH,12
	AOSN LIFF
	POPJ P,
	SKIPGE STOPF
	JRST GOX
	SKIPG TICC
	JRST .-3
	MOVE CH,TIOP
	CAMN CH,[(10700)TIBE-1]
	MOVEI CH,TIBO-1
	HRRM CH,TIOP
	ILDB CH,TIOP
	CAIN CH,15
	SETOM LIFF
	SOS TICC
	POPJ P,

TYO:	CAIN CH,11
	JRST TYO2
	CAIN CH,15
	SETOM TABCNR
	CAIE CH,12
	AOS TABCNR
	SKIPG TORM
	JRST .-1
	IDPB CH,TOIP
	SOS TORM
	EXCH CH,TOIP
	CAMN CH,[(10700)TOBE-1]
	HRRI CH,TOBO-1
	EXCH CH,TOIP
STYO:	CONSO TTY,30
	CONO TTY,10+TTYCHN
	POPJ P,

TYO2:	MOVEI CH,40
	PUSHJ P,TYO
	MOVE CH,TABCNR
	TRNE CH,7
	JRST TYO2
	MOVEI CH,11
	POPJ P,
PITELE:	CONSZ TTY,10
	JRST TYP1
	CONSO TTY,40
	JRST TTYRET
	DATAI TTY,A
	JUMPE A,PITEL3
	TRZ A,200
	SKIPLE BRKFLG
	JRST TTYRET
PITL4A:	CAIE A,33
	CAIN A,176
	MOVEI A,ALTMOD
	MOVEM B,PITEBS
	MOVE B,ECHOCC
	ADD B,TICC
	CAIL B,<TIBE-TIBO-2>*5
	TLO A,400000
	MOVE B,PITEBS
	JUMPL A,DING
	IDPB A,TIIP
	ORCMI A,177
	AOJE A,PITEL1
	CLEARM STOPF
PITEL2:	MOVE A,TIIP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,TIIP
	AOSA ECHOCC
DING:	SETOM DINGF
	PUSHJ P,STYO
	JRST TTYRET

PITEL1:	MOVE A,TIME
	SKIPN STOPF
	MOVEM A,TSTPTM
	AOS A,STOPF
	CAIE A,3
	JRST PITEL2
	MOVE A,TIME
	SUB A,TSTPTM
	CAIL A,20.
	JRST PITEL2-1
	HRROS STOPF
	SETOM FSHFLG
	JRST PITEL2

PITEL3:	AOS BRKFLG
	MOVE A,TIME
	MOVEM A,TSTPTM
	JRST TTYRET

PITEBS:	0
TYP1:	MOVEI A,12
	AOSN LIF
	JRST TYP3
	MOVEI A,7
	AOSN DINGF
	JRST TYP3
	MOVEI A,40
	AOSG SPCCC
	JRST TYP3
	MOVE A,TORM
	CAIL A,TOBS
	JRST TYP4
	MOVE A,TOOP
	CAMN A,[(10700)TOBE-1]
	MOVEI A,TOBO-1
	HRRM A,TOOP
	ILDB A,TOOP
	AOS TORM

TYP3:	CAIN A,10
	JRST TYO1
	CAILE A,6
	CAIL A,16
	JRST TYO1
TYP31A:	CAIE A,11
	JRST TYP31
	MOVNI A,3
	MOVEM A,SPCCC
	JRST TTYRET
TYP31:	CAIN A,ALTMOD
	MOVEI A,"$
	CAIN A,177
	JRST TTYRET
	SKIPL FSHFLG
	DATAO TTY,A
	JRST TTYRET
TYP4:	SKIPG ECHOCC
	JRST TYP5
	MOVE A,ECHOP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,ECHOP
	MOVEI A,12
	HRRM A,TYP1
	ILDB A,ECHOP
	SOS ECHOCC
	AOS TICC
	CAIN A,15
	SETOM LIF
	JRST TYP3
TYP5:	CONO TTY,200+TTYCHN
	JRST POPRET

TYO1:	CAIL A,40
	JRST TYP31A
	ADDI A,100
	HRRM A,TYP1
	SETOM LIF
	MOVEI A,"^
	JRST TYP31
FSHFLG:	0
TOIP:	(10700)TOBO-1
TOOP:	(10700)TOBO-1
TORM:	TOBS
TIIP:	(10700)TIBO-1
TIOP:	(10700)TIBO-1
TICC:	0
ECHOP:	(10700)TIBO-1
ECHOCC:	0
TIBO:	BLOCK 26.
TIBE:
TOBO:	BLOCK 40
TOBE:
TOBS=<TOBE-TOBO>*5
LIF:	0
DINGF:	0
SPCCC:	0
TSTPTM:	0
]

STOPF:	0

IFN TS,[
TYI:	MOVEI CH,12
	AOSE LIFF
TYIW:	IOT TYIC,CH
	CAIN CH,15
	SETOM LIFF
	CAIN CH,^G
	JRST TYIW
CCRR:	POPJ P,CRR

TYO:	SKIPE STOPF
	POPJ P,
	CAIE CH,^T
	CAIN CH,14
	SKIPN GETTY
	JRST TYO3
	IOT TYOC,["_]
	ADDI CH,100
	IOT TYOC,CH
	POPJ P,

TYO3:	IOT TYOC,CH
	SKIPE GETTY
	TRO FF,VIEWF
	POPJ P,

CTLL:	SKIPE GETTY
	IOT DCHN,[14]
	POPJ P,
]
PPA:IFN LPTR,[
	TLNE FF,LPTF
IFE TS,	PUSHJ P,APILPT
IFN TS,	.IOT LPTC,CH
]
PPA2:	TLNE FF,UWRITE
	PUSHJ P,UTYO
IFN TS,POPJ P,
IFE TS,[TLNE FF,7W
	PUSHJ P,7WCH
	TLNE FF,PNSTOP
	POPJ P,
	IORI CH,200
	CAIN CH,214
	JRST PPA1
PPA3:	PUSH P,CH
	PUSHJ P,PIPUN
	POP P,CH
	CAIE CH,211
	POPJ P,
	MOVEI CH,377
	PUSHJ P,PPA2+2
	JRST PPA2+2

UTYO:	EXCH A,CH
	PUSHJ P,UWR"
	TYPR1 [ASCII ?TAPE FULL!?]
	EXCH A,CH
]
IFN TS,[UTYO:	IDPB CH,UTYOP
	AOSGE UTYOCT
	POPJ P,
UTYOA:	MOVEM CH,UTYOP
	MOVNI CH,<UTOBE-UTOBUF>*5
	MOVEM CH,UTYOCT
	MOVE CH,[UTOBUF-UTOBE,,UTOBUF]
	IOT UTYOC,CH
	MOVE CH,[700,,UTOBUF-1]
	EXCH CH,UTYOP
]	POPJ P,

IFE TS,[PPA1:	PUSH P,B
	MOVEI B,12
	PUSHJ P,FEED1
	MOVEI CH,214
	PUSHJ P,PIPUN
	MOVEI CH,377
	MOVEI B,4
	PUSHJ P,FEED1+1
	MOVEI B,30
	PUSHJ P,FEED1
	POP P,B
	POPJ P,
]
CRR:	MOVEI CH,TYO
	HRRM CH,LISTF5

CRR1:	MOVEI CH,15
	PUSHJ P,@LISTF5
	MOVEI CH,12
	JRST @LISTF5

SKRCH:	SKIPG COMCNT
	TYPR2 1,(SIXBIT \UEC\)	;UNEXPECTED END OF COMMAND
RCH:	SOSGE COMCNT
	JRST RCH2
	ILDB CH,CPTR
TRACS:	POPJ P,TYO	;OR JRST TYO IN TRACE MODE

RCH2:	POP P,CH
	POP P,MARG2
	POP P,MARG1
	POP P,COMCNT
	POP P,CPTR
	POP P,COMAX
	PUSH P,CH
	JRST RCH

SKRCH1:	SOSGE COMCNT
	TYPR2 2,(SIXBIT \UEC\)	;ALSO
	ILDB CH,CPTR
	POPJ P,

CLIS:	PUSHJ P,CRR
LIS:	CLEARM COMCNT
	CLEARM INTDPH
	CLEARM SYMS
	MOVE T,[SYMS,,SYMS+1]
	BLT T,SYMEND-1
	MOVE AA,[170700,,CBUF]
	MOVE B,CBUFH
LI1:	TRZ FF,ALTF
LI2:	CAILE B,(AA)
	JRST LI3
IFE TS,	CLEARM BARPNT
	ADDI B,100
	MOVE C,Z
	IDIVI C,5
	MOVE D,QRBUF
	IDIVI D,5
	SUBM C,D
	ADDI C,CBUF
	MOVE CH,(C)
	MOVEM CH,100(C)
	SOS C
	SOJGE D,.-3
	MOVEI C,500
	ADDM C,BEG
	ADDM C,PT
	ADDM C,Z
	ADDM C,QRBUF

LI3:	MOVEM B,CBUFH
	PUSHJ P,TYI
	TRZE FF,QMFLG
	CAIE CH,77
	AOSA COMCNT
	JRST ERRTYP
LI3B:	IDPB CH,AA
	CAIE CH,177
	JRST LI4
	IBP AA
	IBP AA
	IBP AA
	SOS D,AA
	CAMN AA,[260700,,CBUF]
	JRST CLIS
	ILDB CH,D
	PUSHJ P,TYO
	SOS COMCNT
	SOS COMCNT
	JRST LI1
LI4:	CAIE CH,ALTMOD
	JRST LI1
	TRON FF,ALTF
	JRST LI2
	MOVEI CH,GLITCH
	AOS A,COMCNT
	MOVEM A,COMAX
	ADDI A,3
	IDPB CH,AA
	MOVE AA,[(700)CBUF-1]
	IDPB CH,AA
	ROT A,-7
	IDPB A,AA
	ROT A,7
	IDPB A,AA
	MOVEM AA,CPTR
	PUSHJ P,CRR
IFE TS,	CONO DIS,0
IFN TS,[SKIPN GETTY
	DSTOP
]	JRST CD

ERRTYP:	MOVE AA,ERR2
	MOVEI B,12
	SUBI AA,2
	ILDB CH,AA
	CAMG B,ERR1
	PUSHJ P,TYO
	CAME AA,ERR2
	SOJA B,.-4


INI:	JSR ERR
1INS:	SKIPL DTB(CH)	;NEGATIVE INSTRUCTIONS DO NOT RETURN VALUE
	JRST VALRET	;POSITIVE ONES RETURN VALUE
CD:
RET:	SKIPGE STOPF
	JRST GOX
	TRZ FF,ARG2+ARG+FINDR
CD1:	CLEARM NUM
CD2:	MOVSI A,(ADD B,)
CD3:	HLLM A,DLIM
CD4:	CLEARM SYL
	CLEARM OSYL
CD5:	MOVEI AA,0
	PUSHJ P,RCH
	TRZ FF,FINF+PANIC
	XCT DTB(CH)
	JUMPE AA,1INS	;IF AA NOT CHANGED, THIS IS ONE INST. SUBR.
CD6:	MOVE B,NUM
	TRZE FF,SYLF
DLIM:	ADD B,SYL
	MOVEM B,NUM
	MOVE C,SARG
	TRZ FF,VIEWF+NOTF+PCHFLG+FINDR
	JUMPGE AA,(AA)
	PUSHJ P,(AA)
	JRST RET

CDNUM:	MOVE A,OSYL
	LSH A,3
	ADDI A,-60(CH)
	MOVEM A,OSYL
	MOVE A,SYL
	IMULI A,12
	ADDI A,-60(CH)
VALRET:	MOVEM A,SYL
CD7:	TRO FF,ARG+SYLF
	JRST CD5

COMMA:	MOVEM B,SARG
	TRZE FF,ARG
	TROE FF,ARG2
	TYPR2 1,(SIXBIT \WNA\)
	JRST CD1

CAND:	MOVSI A,(AND B,)
	JRST CD3
COR:	MOVSI A,(IOR B,)
	JRST CD3
MINUS:	MOVSI A,(SUB B,)
	JRST CD3
TIMES:	MOVSI A,(IMUL B,)
	JRST CD3
SLASH:	MOVSI A,(IDIV B,)
	JRST CD3

HOLE:	CLEARM SARG
	TRO FF,ARG2
END1:	MOVE A,Z
	SUB A,BEG
	JRST VALRET

OPEN:	PUSH P,NUM
	HLLZ A,DLIM
	TRZE FF,ITERF
	IORI A,1
	PUSH P,A
	AOS LEV
	JRST RET

CLOSE:	SOSGE LEV
	TYPR2 1,(SIXBIT \UMC\)	;UNMATCHED CLOSE PAREN
	MOVEM B,SYL
	POP P,CH
	HLLM CH,DLIM
	TRZ FF,ITERF
	TRNE CH,1
	TRO FF,ITERF
	POP P,NUM
	JRST CD7

CHK:	CAMG B,Z
	CAMGE B,BEG
	TYPR2 1,(SIXBIT \NIB\)	;NOT IN BUFFER
CRDPT:	POPJ P,RDPT

CHK1:	CAMG C,BEG
	MOVE C,BEG
	CAML B,Z
	MOVE B,Z
	CAMLE C,B
	TYPR2 (SIXBIT \2<1\)	;2ND ARG LESS THAN 1ST
IFE TS,[CCRR:]	POPJ P,CRR

PNT:	MOVE A,OSYL
	TRNE FF,SYLF
	JRST VALRET
	MOVE A,PT
	JRST END1+1
APPEND:	TRNE FF,ARG
	JRST APPND1
	MOVE OUT,Z
	PUSHJ P,YANK2
	JRST RET
APPND1:	ADD B,PT
	SOS IN,B
	PUSHJ P,CHK
	PUSHJ P,GET
	MOVE A,CH
APPND2:	CLEARM NUM
	JRST VALRET

YANK:YANK1:	MOVE OUT,BEG
	MOVEM OUT,PT
YANK2:IFN TS,[TLNE FF,UREAD
	JRST YNKF
]
YANK3:	MOVE F,MEMSIZ
	SUB F,OUT
	CAIG F,40.*5
	JRST YANK6	;PANIC STOP
	CAIG F,200.*5
	TRO FF,PANIC	;LOOK FOR LINE FEED
IFE TS,[TLNN FF,UREAD+7R
	JRST YANK8
]	TLNN FF,UREAD
IFE TS,	PUSHJ P,7RCH
IFN TS,	TYPR2 (SIXBIT \NDO\)
IFE TS,	TLNE FF,UREAD
YANK4:	PUSHJ P,UTYI
	PUSHJ P,PUT
	TRNE FF,ARG
	SOJLE B,YANK7
	TRNN FF,PANIC
	JRST .+4
	CAIE CH,13
	CAIN CH,12
	JRST YANK6	;FOUND LF OR VT
	CAIE CH,14
	AOJA OUT,YANK3
YANK5:	MOVEM OUT,Z
IFE TS,	CAMN OUT,BEG
	TLNE FF,UREAD
	POPJ P,
IFE TS,[MOVN OUT,RCC
	CAIN OUT,10
	TRO FF,FINF
	POPJ P,
]YANK6:	ITYPR (SIXBIT \URK\)	;BUFFER GETTING BLOATED
YANK7:	AOJA OUT,YANK5

IFE TS,[YANK8:	TLNN FF,7R
	JRST YANK4
	PUSHJ P,PIRPA
	JRST YANK4+1
]DEFINE DBP7 A
	ADD A,[70000,,]
	SKIPGE A
	SUB A,[430000,,1]
TERMIN

IFN TS,[
YNKF:	SETZM UTIEF
	PUSH P,E
	MOVE F,OUT
	PUSHJ P,GETBP
	MOVE D,[YPG,,A]
	BLT D,C
	MOVE D,UTYIP
	JRST A

YPG:	ILDB CH,D	;A
	CAIN CH,14	;AA
	JRST YPG1	;B
	IDPB CH,F	;E
	JRST A	;C

YPG1:	MOVEM D,UTYIP
	HRRZ CH,D
	CAIN CH,UTIBE
	JRST YPG2	;JUST EOB
	DBP7 D
	LDB CH,D
	CAMN CH,SYSEOF'
	JRST YPG3	;EOF
YPG1A:	PUSHJ P,GETCA
	AOS OUT,F
	POP P,E
	JRST YANK5

YPG2:	PUSHJ P,UTRLD
	SKIPL UTIEF
	JRST YPG2A
	MOVE D,UTYIP
	ILDB TT1,D
	CAME TT1,SYSEOF
	JRST .-2
	MOVEI TT1,14
	IDPB TT1,D
YPG2A:	TRNE FF,FINF
	JRST YPG3
	DBP7 F
	MOVE D,UTYIP
	JRST A+1

YPG3:	PUSHJ P,UTYIA
	DBP7 F
	JRST YPG1A

GETBP:	MOVE TT,F
	IDIVI TT,5
	MOVE F,BTAB(TT1)
	HRRI F,CBUF(TT)
	DBP7 F
	TLZ F,17
	POPJ P,
]

REVERS:	PUSHJ P,CHK2
	MOVNS B
CHARAC:	PUSHJ P,CHK2
	ADD B,PT
JMP1:	PUSHJ P,CHK
	MOVEM B,PT
	JRST RET
JMP:	ADD B,BEG
	JRST JMP1
CHK2:	TROE FF,ARG
	POPJ P,
	LDB B,[(340200)DLIM]
	MOVNS B
	AOJA B,CPOPJ

KILL:	PUSHJ P,GETARG
	PUSHJ P,CHK1
	MOVEM C,PT
	SUB B,C
	JUMPE B,RET
DELETE:	PUSHJ P,CHK2
	MOVM C,B
	MOVNS C
	ADD B,PT
	PUSHJ P,CHK
	CAMGE B,PT
	MOVEM B,PT
	PUSHJ P,NROOM
DEL2:
JRET:	JRST RET


IFE TS,[FEED:
FEED1:	MOVEI CH,0
	PUSHJ P,PPA3
	SOJG B,.-1
	POPJ P,

UTYI:	PUSHJ P,URED"
	TRO FF,FINF
	MOVE CH,A
	POPJ P,
]

IFN TS,[
UTYI:	ILDB CH,UTYIP
	CAME CH,SYSEOF
	POPJ P,
UTYIA:	HRRZ CH,UTYIP
	CAIN CH,UTIBE
	JRST UTRLD
UICLS:	TRO FF,FINF
	TSCLOSE UTYIC,
	SETOM ERDEV
	MOVE CH,[160700,,[EOFCHR_7+14]]
	MOVEM CH,UTYIP
	MOVEI CH,EOFCHR
	MOVEM CH,SYSEOF
	MOVEI CH,14
	POPJ P,

UTRLD:	MOVE CH,[UTIBUF-UTIBE,,UTIBUF]
	IOT UTYIC,CH
	JUMPGE CH,UTRLD1
	SETOM UTIEF
	HRLI CH,14_4.
	PUSH P,A
	MOVE A,SYSEOF
	DPB A,[350700,,CH]
	POP P,A
	HLLM CH,(CH)
UTRLD1:	MOVE CH,[700,,UTIBUF-1]
	MOVEM CH,UTYIP
	JRST UTYI
UTIEF:	0
]

TAB:	PUSHJ P,TAB2
INSERT:	TRNE FF,ARG
	JRST INS1A
	MOVEI CH,ALTMOD
	TRZE FF,SLSL
	PUSHJ P,RCH
	MOVEM CH,A
	MOVE B,CPTR
	MOVEI C,0
	PUSHJ P,SKRCH
	CAME CH,A
	AOJA C,.-2
	PUSHJ P,NROOM
	ADD B,CRREL
INS1B:	MOVE OUT,PT
	ILDB CH,B
	CAMN CH,A
	POPJ P,
	PUSHJ P,PUT
	AOS CH,PT
	JRST INS1B


INS1A:
TAB1:	MOVE CH,NUM
TAB2:	MOVEI C,1
	PUSHJ P,NROOM
	AOS OUT,PT
	SOJA OUT,PUT

TYOM:	PUSH P,C
	PUSH P,OUT
	PUSH P,TT
	PUSH P,TT1
	PUSHJ P,TAB2
	POP P,TT1
	POP P,TT
	POP P,OUT
	POP P,C
	POPJ P,

LARR:	TROA FF,FINDR
SERCHP:	TRO FF,PCHFLG
SERCH:	SETOM V605'
	TRZ FF,FINF
	MOVE E,B
	CLEARM SYL
	CLEARM NUM
	MOVEI CH,ALTMOD
	TRZE FF,SLSL
	PUSHJ P,RCH
	MOVEM CH,SBRK'
	MOVEI F,GCTAB	;SEARCH TABLE FORMAT
SRCH1:	HRRZS TT,F	;.+4,,.
SRCH2:	PUSHJ P,RCH	;CAIN CH,"F
	CAMN CH,SBRK	;CAIE CH,"O
	JRST SRLP1	;CAIN CH,"O
	CAIN CH,^O	;.+6,,.
	JRST SRLP2	;CAIN CH," 	;SPACE
	CAIN CH,^X	;CAIE CH,"S
	JRST SRLP3	;CAIN CH,"P
	CAIN CH,^N	;PUSHJ P,CNTRB1
	JRST SRLP4	;CAIA CH,^X
	CAIN CH,^B	;CAIN CH,")
	JRST SRLP5	;MEANS SF^NOO^O ^NSP^N^B^X)$$
	CAIN CH,^Q
	PUSHJ P,RCH
	HRLI CH,(CAIN CH,)
SRCH3:	TRZE FF,NOTF
	TLC CH,(CAIE#CAIN)
	MOVEM CH,(F)
	AOJA F,SRCH2

SRLP1:	SETOM SBRK
SRLP2:	HRL F,TT
	AOS A,V605
	CAIL A,40
	TYPR2 (SIXBIT \STL\)	;SEARCH STRING TOO LONG
	MOVEM F,STAB(A)
	SKIPL SBRK
	JRST SRCH1
SRLP6:	PUSH P,PT
SRLP2A:	CLEARM V607'
	SETOM V610'
SRLP3A:	MOVE A,V607
	MOVE F,STAB(A)
	MOVE IN,(P)
	MOVEM IN,PT
	TRNE FF,ARG
	JUMPLE E,SRLP7
SRLP4A:	CAML IN,Z
	JRST SRLP8
	HLRZ D,F
SRLP5A:	CAIN D,(F)
	JRST SRLP6A
	PUSHJ P,GETINC
	XCT (D)
SRLP5B:	AOJA D,SRLP5A
SRLP5C:	AOS IN,PT
	JRST SRLP4A

SRLP6A:	CAMLE IN,Z
	JRST SRLP8
SRLP7:	SETOM SFINDF
	SKIPL V610
	CAMGE IN,V610
	JRST SRLP9
SRLP8:	AOS A,V607
	CAMG A,V605
	JRST SRLP3A
	SKIPGE IN,V610
	JRST SRLP10
	MOVEM IN,(P)
	SOJG E,SRLP2A
	POP P,PT
	TRZN FF,COLONF
	JRST RET
	SETCM A,SYL
	JRST VALRET

SRLP9:	MOVEM IN,V610
	MOVE A,V607
	MOVEM A,SYL
	JRST SRLP8
SRLP10:	MOVE IN,BEG
	MOVEM IN,PT

LOSE:	CLEARM SFINDF
	SUB P,[1,,1]
	TRNE FF,PCHFLG+FINDR
	JRST LOSE1
BEGIN1:	TRZN FF,COLONF
	JRST LOSE2
BEGIN2:	TRZ FF,PCHFLG+FINDR
BEGIN:	MOVEI A,0
	JRST VALRET

LOSE1:	TRNE FF,FINF
	JRST BEGIN1
	PUSH P,E
	MOVEI B,1
	TRNE FF,PCHFLG
	PUSHJ P,PUNCHA
	TRNE FF,FINDR
	PUSHJ P,YANK1
	POP P,E
	JRST SRLP6

LOSE2:	TRNE FF,ITERF
	JRST BEGIN2
	TYPR2 (SIXBIT \SFL\)	;SEARCH FAILED

SRLP5:	SKIPA CH,[JSR P,CNTRB1]
SRLP3:	MOVSI CH,(CAI)
	JRST SRCH3

IFN TS,[
GETCA:	LDB TT,[360600,,F]
	TLZ F,-1
	IMULI F,5
	IDIVI TT,7
	SUBI F,CBUF*5-4(TT)
	POPJ P,
]

CNTRB1:	JRST DQT3
	MOVE A,[JRST DQT3]
	MOVEM A,CNTRB1
	PUSHJ P,DQT3
	JRST SRLP5C
	JRST SRLP5B

SRLP4:	TRO FF,NOTF
	JRST SRCH2
TYPE:
TYPE4:	MOVEI D,TYO
	PUSHJ P,GETARG
TYPE1:	PUSHJ P,CHK1
	MOVE IN,C
IFN TS,[	MOVEI A,PPA
	CAIN A,(D)
	TLNE FF,LPTF
	JRST TYPE3
	TLNE FF,UWRITE
	JRST PUNCHF
]
TYPE3:	CAML IN,B
	JRST TYPE5
	PUSHJ P,GETINC
	PUSHJ P,(D)
IFE TS,[SKIPGE BRKFLG
	JRST TYPE6]
	JRST TYPE3
TYPE5:	MOVEI A,PPA
	MOVEI CH,14
	CAIE A,(D)
	POPJ P,
CPPA:	JRST PPA
TYPE6:	MOVEM IN,PT
	JRST GOX
USE:	TRNN FF,ARG
	TYPR2 2,(SIXBIT \WNA\)
USEA:	PUSHJ P,QREGA
	MOVEM B,QTAB-60(CH)
	JRST RET
QREG:	PUSH P,USE1
QREGA:	PUSHJ P,RCH
	CAIL CH,"0
	CAILE CH,"Z
	TYPR2 (SIXBIT \IQN\)
	CAIL CH,"9+1
	SUBI CH,"A-"9-1
	MOVE A,QTAB-"0(CH)
USE1:	POPJ P,VALRET


NQREG=="Z-"A+1 "9-"0+1

IFN TS,[PUNCHF:	CAML IN,B
	JRST TYPE3
	MOVE F,IN
	SUBM B,IN
	PUSHJ P,GETBP
	PUSH P,E
	MOVE C,[PPG,,A]
	BLT C,E
PCHF1:	MOVE C,UTYOP
	MOVN OUT,UTYOCT
	CAMLE OUT,IN
	MOVE OUT,IN
	PUSH P,OUT
	JUMPE OUT,PPG1
	JRST A

PPG:	ILDB CH,F	;A
	IDPB CH,C	;AA
	SOJG OUT,A	;B
	JRST PPG1	;E

PPG1:	POP P,OUT
	MOVEM C,UTYOP
	ADDM OUT,UTYOCT
	SKIPL UTYOCT
	PUSHJ P,UTYOA
	SUB IN,OUT
	JUMPG IN,PCHF1
	POP P,E
	JRST TYPE5
]
PUNCH:
PUNCHA:	IFN TS,[
	TLNN FF,LPTF\UWRITE
	TYPR2 (SIXBIT /NDO/)
]	MOVEI D,CPPA
	TRNE FF,ARG2
	JRST TYPE1-1
	MOVE E,B
	MOVE B,CPTR
	ILDB T,B
	JUMPL E,CPOPJ
PUN1:	PUSHJ P,PUNCHR
	TRZ FF,ARG
	SKIPE COMCNT
	CAIE T,"W
	PUSHJ P,YANK1
	MOVE C,Z
	CAMN C,BEG
	TRNN FF,FINF
	SOJG E,PUN1
CPOPJ:	POPJ P,VIEW1
PUNCHR:	SKIPGE STOPF
	JRST GOX
	MOVE C,BEG
	MOVE B,Z
	MOVEI D,PPA
	JRST TYPE1

DPT2:	MOVNS B
	TLO FF,NEGF
RDPT:	SOJA TT,DPT1
DPT:	TDZA TT,TT
SLDPT:	MOVEI TT,2
DPT1:	JUMPL B,DPT2
	IDIVI B,10.
	HRLM E,(P)
	SKIPE B
	PUSHJ P,RDPT
	TLZE FF,NEGF
	PUSH P,["--"0,,DPT3]
DPT3:	JUMPLE TT,DPT4
DPT5:	MOVEI CH,40
	PUSHJ P,@LISTF5
	SOJG TT,.-1
DPT4:	HLRZ CH,(P)
	ADDI CH,"0
	JRST @LISTF5

PRNT:	TRNN FF,ARG
	TYPR2 3,(SIXBIT \WNA\)
	PUSH P,CCRR
	MOVEI A,TYO
	HRRM A,LISTF5
	JRST DPT

DD:	IFN TS,[
	TRNN FF,ARG
	JRST DD1
]	ANDI B,3
	ROT B,4
	MOVEM B,CHSIZ
	JRST RET

IFN TS,[
DD1:	SETCMM DSW'
	JRST RET
]

DQUOTE:	TRNN FF,ARG
	TYPR2 4,(SIXBIT \WNA\)
	PUSHJ P,RCH
	MOVSI A,0
	IRPC Z,,[GLNE]
	CAIN CH,"Z
	MOVSI A,(JUMP!Z B,)
	TERMIN
	CAIN CH,"C
	JRST DQT1
	JUMPE A,ERR+1
	HRRI A,RET
	XCT A
NOGO:	MOVEI A,0
	PUSHJ P,SKRCH1
	CAIN CH,42
	AOJA A,.-2
	CAIN CH,47
	SOJL A,RET
	JRST .-5
EXCLAM:	PUSHJ P,SKRCH
	CAIE CH,"!
	JRST .-2
	JRST RET

IFN TS,[
LRCH:	PUSHJ P,SKRCH
	CAIL CH,140
	SUBI CH,40
	POPJ P,
]DQT1:	PUSHJ P,DQT2
	JRST RET
	JRST NOGO

DQT3:	MOVE B,CH
DQT2:	CAIE B,"$
	CAIN B,"%
	POPJ P,
	CAIN B,".
	POPJ P,
	CAIGE B,"0
	JRST POPJ1
	CAIG B,"9
	POPJ P,
	CAIGE B,"A
	JRST POPJ1
	CAIG B,"Z
	POPJ P,
	JRST POPJ1

OG:	MOVE A,CPTR
	MOVE AA,A
	IDIVI AA,17
	CAMN A,SYMS(B)
	JRST OGFND
	SKIPN SYMS(B)
	JRST OGNF
	CAMN A,SYMS+1(B)
ES1:	AOJA B,OGFND
	SKIPN SYMS+1(B)
ES2:	AOJA B,OGNF
	CAMN A,SYMS+2(B)
	AOJA B,ES1
	SKIPN SYMS+2(B)
	ADDI B,2

OGNF:	PUSH P,CPTR
	PUSH P,B
	MOVEI D,STAB+1
	MOVEI A,41
	MOVEM A,-1(D)
	PUSHJ P,SKRCH
	MOVEM CH,(D)
	CAIE CH,ALTMOD
	AOJA D,.-3
	MOVEM A,(D)
	MOVE B,COMCNT
	SUB B,COMAX
	IDIVI B,5
	ADD B,CPTR
	JUMPE E,OG2
	SOS B
	MOVMS E
	JRST .(E)
	IBP B
	IBP B
	IBP B
	IBP B
OG2:	MOVE AA,COMAX
OG4:	MOVEM B,CPTR
	MOVEM AA,COMCNT
	MOVEI E,STAB
OG5:	CAIN E,1(D)
	JRST OG3
	PUSHJ P,SKRCH1
	CAMN CH,(E)
	AOJA E,OG5
	IBP B
	SOJA AA,OG4

OG3:	POP P,A
	POP P,SYMS(A)
	MOVEM AA,CNTS(A)
	MOVEM B,VALS(A)
	JRST RET

PCNT:	PUSHJ P,QREGA
	AOSA A,QTAB-60(CH)
LAT:	DATAI A
	JRST VALRET

IFN TS,[
RTIMER:	.RDTIME A,
	LSH A,1
	JRST VALRET
]

OGFND:	MOVE A,VALS(B)
	MOVEM A,CPTR
	MOVE A,CNTS(B)
	MOVEM A,COMCNT
	JRST RET
CHSIZ:	20
VIEW:	TRO FF,VIEWF
	PUSHJ P,DISINI
	PUSH P,CPOPJ	;POPJ P,VIEW1
	MOVEI D,DISAD
	TRNE FF,VIEWF
	JRST TYPE4+1
	JRST TYPE1

VIEW1:	PUSHJ P,DISCLG
	MOVE T,CPTR
	ILDB B,T
	TRNN FF,VIEWF
	JRST GO1
	SKIPE COMCNT
	CAIE B,"W
	JRST RET
	PUSHJ P,TYI
	MOVE A,CH
IFE TS,	CONO DIS,0
IFN TS,[SKIPN GETTY
	DSTOP
]	PUSHJ P,RCH
	JRST APPND2

VIEW2:IFE TS,CONO DIS,0
IFN TS,[SKIPN GETTY
	DSTOP
	TLZE FF,CTLUF
	JRST CNTRLV
]	MOVE B,NLINES
	PUSHJ P,GETAG7
	PUSH P,B
	MOVN B,NLINES
	PUSHJ P,GETAG7
	POP P,B
	JRST VIEW+1

NLINES:	20.


ERR:	0
ERRP1:
IFE TS,	PUSHJ P,UWAIT"
	MOVEI CH,277
	PUSHJ P,TYO
	PUSHJ P,CRR
	TRO FF,QMFLG
	MOVE A,COMAX
	SUB A,COMCNT
	MOVEM A,ERR1
	MOVE A,CPTR
	MOVEM A,ERR2
	JRST GO

CNTRUP:	PUSHJ P,RCH
	MOVE A,CH
	JRST VALRET
QUESTN:	MOVSI A,(JRST)
	TRCE FF,TRACEF
	MOVSI A,(POPJ P,)
	HLLM A,TRACS
	JRST RET

IFN TS,[
CONSET:	TRNN FF,ARG
	TYPR2 (SIXBIT /WNA/)
	JUMPL B,GCLOSE
	ADDI B,20
	DPB B,[(600)CONOP]
	TSOPEN DCHN,CONOP
	MOVEI B,9.
	MOVEM B,NLINES
	SETOM GETTY
	JRST RET

GCLOSE:	TSOPEN DCHN,OTTY
	MOVE A,RGETTY
	MOVEM A,GETTY
	JRST RET

CONOP:	5,,(SIXBIT /TY /)
	SIXBIT /.TECO./
	SIXBIT /DISOUT/
]
BAKSL:	TRZE FF,ARG
	JRST BAKS1A
	MOVE IN,PT
	PUSHJ P,GETINC
	CAIE CH,"-
	JRST BAKSL7
	TRO FF,ARG
	JRST BAKSL6
BAKSLA:	PUSHJ P,GETINC
BAKSL7:	CAMLE IN,Z
	JRST BAKSL3
BAKSL6:	CAIN CH,".
	JRST BAKSL5
	CAIG CH,"9
	CAIGE CH,"0
	SOJA IN,BAKSL2
	MOVE A,SYL
	IMULI A,10.
	ADDI A,-60(CH)
	MOVEM A,SYL
	MOVE A,OSYL
	IMULI A,10
	ADDI A,-60(CH)
	MOVEM A,OSYL
	JRST BAKSLA
BAKSL5:	MOVE A,OSYL
	MOVEM A,SYL
	JRST BAKSL2
BAKSL3:	MOVE IN,Z
BAKSL2:	TRZE FF,ARG
	MOVNS SYL
	MOVEM IN,PT
	JRST CD7

BAKS1A:	MOVEI TT,40
	HRRM TT,DPT5
	MOVE TT,C
	TRZE FF,ARG2
	SKIPA F,CRDPT
BAKSL1:	MOVEI F,DPT
	MOVE T,[(700)BAKTAB]
	MOVEI C,0
	MOVEI CH,BAKSL4
	HRRM CH,LISTF5
	PUSHJ P,(F)
	MOVEI A,GLITCH
	IDPB A,T
	MOVE B,[(700)BAKTAB]
	PUSHJ P,NROOM
	PUSHJ P,INS1B
	JRST RET

BAKSL4:	IDPB CH,T
	AOJA C,CPOPJ

CLOSEB:	SKIPA C,[POP PF,]
OPENB:	MOVSI C,(PUSH PF,)
	PUSHJ P,QREGA
	HRRI C,QTAB-"0(CH)
	XCT C
	JRST CD5

SEMICL:	TRNN FF,ITERF
	TYPR2 (SIXBIT \SNI\)	;SEMICOLON NOT INSIDE ITERATION
	TRNN FF,ARG
	MOVE B,SFINDF

INCMA:	JUMPL B,CD
	MOVEI A,0
INCMA1:	PUSHJ P,SKRCH1
	CAIN CH,74
	AOJA A,INCMA1
	CAIE CH,76
	JRST INCMA1
	SOJGE A,INCMA1
INCMA2:	SOS INTDPH
	SUB P,[3,,3]
	POP P,ITERCT
	JRST RET

GRTH:	SKIPG INTDPH
	TYPR2 2,(SIXBIT \UMC\)	;UNMATCHED CLOSE ANGLE BRACKET
	TRZ FF,ITERF
	SOSN ITERCT
	JRST INCMA2
	MOVE A,(P)
	MOVEM A,COMCNT
	MOVE A,-1(P)
	MOVEM A,CPTR
	TRNE FF,TRACEF
	PUSHJ P,CRR


LSSTH1:	TROA FF,ITERF
FLDSZ:	MOVEM B,NLINES
	JRST RET

LSSTH:	AOS INTDPH
	PUSH P,ITERCT
	PUSH P,COMAX
	PUSH P,CPTR
	PUSH P,COMCNT
	SETOM ITERCT
	TRZE FF,ARG
	MOVEM B,ITERCT
	SKIPE ITERCT
	JRST LSSTH1
	JRST INCMA1

LINE:	TRNE FF,ARG2
	TYPR2 5,(SIXBIT \WNA\)	;WRONG NO OF ARGS
	PUSHJ P,GETARG
	XOR B,C
	XORM B,PT
	JRST RET

IFE TS,[DECDMP:	PUSHJ P,IOWAIT
	CONO 435550
	MOVE A,CPTR
	ILDB CH,A
	CAIN CH,ALTMOD
	JRST MACDMP
	MOVE T,[440700,,IFE MOBY,[37760
]IFN MOBY,[40000+MOBY
]]	MOVEM T,MACCR
DCDMP2:	PUSHJ P,RCH
	CAIN CH,"$
	MOVEI CH,375
	IDPB CH,T
	CAIE CH,ALTMOD
	JRST DCDMP2
	MOVEI CH,15
	DPB CH,T
	MOVEI CH,0
	IDPB CH,T
	JRST MACDMP+1

IOWAIT:
IFN LPTR,	PUSHJ P,LPTWAT"
	PUSHJ P,UWAIT"
	CONSZ PTP,7
	JRST IOWAIT
TTYWAT:	CONSO TTY,30
	CONSZ TTY,30
	JRST TTYWAT
	POPJ P,

ADDT:	HLRZ A,DDT
	CAIE A,(CLEARM)
	JRST TAB
	PUSHJ P,IOWAIT
	JRST DDT
W=PUSHJ P,IOWAIT	;WAIT FOR IO WITH W$X
]
R=POPJ P,	;RETURN FROM DDT WITH R$X

IFN TS,[
DECDMP:	MOVE A,CPTR
	ILDB CH,A
	CAIE CH,ALTMOD
	JRST VCOM
	TSCLOSE TYIC,
	TSCLOSE TYOC,
	TSCLOSE UTYIC,
	TSCLOSE UTYOC,
	TSCLOSE LPTC,
	TSCLOSE DCHN,
	SKIPN GETTY
	DFLUSH
	TSVALRET
	JRST GO

VCOM:	MOVE T,[440700,,BFDR]
VCOM2:	PUSHJ P,RCH
	CAIN CH,ALTMOD
	JRST VCOMX
	CAIN CH,"$
	MOVEI CH,ALTMOD
	IDPB CH,T
	JRST VCOM2

VCOMX:	MOVEI CH,EOFCHR
	IDPB CH,T
	TSVALRET BFDR
	JRST RET
]
MAC:	PUSHJ P,QREGA
	TLNE A,400000
	TLNE A,377777
	JRST VALRET
	ADD A,QRBUF
	HRR IN,A
	PUSHJ P,GETINC
	CAIE CH,GLITCH
	JRST CD5
	PUSH P,COMAX
	PUSH P,CPTR
	PUSH P,COMCNT
	PUSH P,MARG1'
	PUSH P,MARG2'
	MOVE B,NUM
	TRNE FF,SYLF
	XCT DLIM
	TRNN FF,ARG
	MOVEI B,0
	MOVEM B,MARG2
	MOVE B,SARG
	TRNN FF,ARG2
	MOVEI B,0
	MOVEM B,MARG1	;SIGH
	PUSHJ P,GETINC
	MOVE A,CH
	PUSHJ P,GET
	ROT CH,7
	IOR A,CH
	SUBI A,3
	MOVEM A,COMCNT
	MOVEM A,COMAX
	ADDI IN,CBUF*5
	IDIVI IN,5
	MOVE OUT,BTAB(OUT)
	HRR OUT,IN
	TLZ OUT,17
	MOVEM OUT,CPTR
	JRST CD5


X:	PUSHJ P,QREGA
	PUSH P,CH
	CLEARM QTAB-"0(CH)
	PUSHJ P,GETARG
	CAMLE C,B
	TYPR2 (SIXBIT \2<1\)
	EXCH B,C
	SUBI C,-3(B)	;C HAS NO. CHARS TO X AWAY + 3
	ADD B,C	;1ST THREE CHARS ARE GLITCH, REMAINDER, LENGTH STRING/7
	PUSH P,PT
	ADDM C,(P)
	MOVE D,BEG
	MOVEM D,PT
	PUSHJ P,NROOM
	MOVE OUT,RREL
	ADDM OUT,(P)
	ADD B,OUT
	MOVE OUT,BEG
	ADDM C,BEG
	MOVEI CH,GLITCH
	PUSHJ P,PUTINC
	MOVE CH,C
	PUSHJ P,PUTINC
	ROT CH,-7
	MOVE IN,B
X1:	PUSHJ P,PUTINC
	CAIN C,3
	JRST X2
	PUSHJ P,GETINC
	SOJA C,X1
X2:	MOVE B,PT
	SUB B,QRBUF
	TLO B,400000
	POP P,PT
	POP P,CH
	JRST USEA+1

IFN TS,[

COREX:	.GMEMT A,	;EXPAND CORE (CAN ONLY CLOBBER AC A)
	LSH A,-10.	;CALLED BY GC AND TSINT
	AOS A
	.CORE (A)
	POPJ P,
	AOS (P)
COREX3:	IMULI A,2000
	SUBI A,CBUF
	IMULI A,5
	MOVEM A,MEMSIZ
	POPJ P,

CORLES:	SETOM GCFLG	;CALLED AT GO,CLOBBERS WHAT IT WANTS
CORL1:	MOVE A,Z
	ADDI A,4
	IDIVI A,5
	MOVEI B,CBUF(A)
	MOVE A,NLINES
	IMULI A,2*80./6
	MOVE C,Z
	SUB C,BEG
	IDIVI C,6
	CAML A,C
	MOVE A,C
	SKIPN GETTY
	ADD B,A
	.GMEMT A,
	SUB A,B
	CAIL A,2000
	JRST CORL2	;SPACE AT TOP
	MOVE C,BEG
	IDIVI C,5
	AOSN GCFLG
	CAIGE C,2000
	POPJ P,
	MOVEM PF,AC2+PF-2	;GC
	PUSHJ P,GCC
	JRST CORL1

CORL2:	MOVEI A,1777(B)
	LSH A,-10.
	.CORE (A)
	.VALUE
	JRST COREX3
]

QGET:	PUSHJ P,QREG+1
	MOVE B,A
	TLZN B,377777
	TLZN B,400000
	JRST BAKSL1
	ADD B,QRBUF
	MOVE IN,B
	MOVE B,CH
	PUSHJ P,GETINC
	CAIE CH,GLITCH
	JRST RET
	PUSHJ P,GETINC
	MOVEM CH,C
	PUSHJ P,GETINC
	ROT CH,7
	IORM CH,C
	SUBI C,3
	PUSHJ P,NROOM
	MOVE OUT,PT
	HRRZ IN,QTAB-"0(B)
	ADD IN,QRBUF
	ADDI IN,3
QGET1:	JUMPE C,RET
	PUSHJ P,GETINC
	PUSHJ P,PUT
	AOS OUT,PT
	SOJA C,QGET1

IFE TS,[CLRBF:	PUSHJ P,PIBUFC
	CONO PTR,0
	JRST RET]

GETARG:	TRNE FF,ARG2
	JRST GETAG6
	TRON FF,ARG
	PUSHJ P,CHK2+2
GETAG7:	MOVE IN,PT
GETAG4:	JUMPLE B,GETAG2
	CAMN IN,Z
	JRST GETAG5
	PUSHJ P,GETINC
	CAIE CH,12
	JRST GETAG4
	SOJG B,GETAG4


GETAG1:	MOVE B,IN
	MOVE C,PT
	POPJ P,

GETAG5:	MOVE B,IN
	JRST GETAG1+1
GETAG6:	ADD B,BEG
	ADD C,BEG
	POPJ P,


GETAG2:	SOS IN
	CAMG IN,BEG
	JRST GETAG3
	PUSHJ P,GETINC
	CAIE CH,12
	SOJA IN,GETAG2
	AOJLE B,.-1
GETAG3:	CAMGE IN,BEG
	MOVE IN,BEG
	MOVE C,IN
	MOVE B,PT
	POPJ P,


DEFINE NTS A/
IFE TS,	A
IFN TS,	TYPR2 (SIXBIT \NTS\)
TERMIN

DEFINE OTS A/
IFE TS,	TYPR2 (SIXBIT \OTS\)
IFN TS,	A
TERMIN

ECMD:IFE TS,PUSHJ P,.OPNTP
ECMD1:IFN TS,[PUSHJ P,GDEV
	TLO FF,CTLUF
]	PUSHJ P,RCH
	CAIN CH,"L
	JRST LISTF
	CAIN CH,"R
	JRST .OPNRD
	CAIN CH,"C
	NTS JRST CLSTP
	CAIN CH,"F
	JRST .FILE
	CAIN CH,"D
	JRST DELE
	CAIN CH,"N
	JRST RENAM
	CAIN CH,"I
	JRST WINIT
	CAIN CH,"K
	JRST TAPKIL"
	CAIN CH,"S
	NTS JRST MTNAM
	CAIN CH,"T
	IFE TS,JRST CNTRU1
	IFN TS,POPJ P,
	CAIN CH,"M
	JRST LISTFM
	CAIN CH,"P
	JRST BPNTRD
IFN TS,[CAIN CH,"W
	OTS JRST WWINIT
	CAIN CH,"Y
	OTS JRST LISTY
	CAIN CH,"A
	OTS JRST EASSIGN
	CAIN CH,"U
	OTS JRST EDESINE
	CAIN CH,^U
	OTS JRST EUHACK
	CAIN CH,"Z
	OTS JRST LISTZ
]	TYPR2 (SIXBIT \IUC\)

IFN TS,[
EIDEV:	-1	;CURRENT DEV SAVED BY EI & EW FOR EF TO RESTORE
ERDEV:	-1	;CURRENT READ DEV
]

IFE TS,[.OPNTP:	MOVE A,B
	TRNN FF,ARG
	MOVE A,UFPNTR+2
.OPN1:	PUSHJ P,FILEST"
	JRST UTERR
	POPJ P,

UTERR:	JRST .+3(A)
	TYPR1 [ASCII /UNIT UNABLE
!/]
	TYPR1 [ASCII /BAD DIRECTORY
!/]
	TYPR1 [ASCII /TOO MANY DIRECTORIES
!/]
]

IFN TS,[GDEV:	TRZN FF,ARG
	POPJ P,
	ANDI B,17
	ADDI B,(SIXBIT \UT0\)
	HRRM B,UTF
	POPJ P,

ASLEEP:	TRZE  FF,ARG
	SLEEP B,
	POPJ P,

EASSIGN:	LDB B,[300,,UTF]
	.ASSIGN B,
	TYPR2 (SIXBIT /UNA/)
	POPJ P,

EDESINE:	LDB B,[300,UTF]
	.DESIGN B,
	TYPR2 (SIXBIT /UNA/)
	POPJ P,

EUHACK:	PUSHJ P,FFRDEV
	JRST CNTRLU
]
BPNTRD:	PUSHJ P,.OPNRD
	JRST .FNPNT

IFN TS,[.OPNRD:	PUSHJ P,FFRRDD
RRED:	HRRZ A,UTF
	HRRZM A,ERDEV
	MOVEI A,2
	HRLM A,UTF
	TSOPEN UTYIC,UTF
	MOVEI CH,UTYIC
	.EOFC CH,
	MOVEM CH,SYSEOF
	DPB CH,[350700,,UTIBE]
	TLO FF,UREAD
	MOVE CH,[700,,UTIBE-1]
	MOVEM CH,UTYIP
	POPJ P,

UCLOSE:	MOVE A,UTOF+1
	SKIPA AA,[SIXBIT /FLAP/]
.FILE:	PUSHJ P,FRD
	MOVEM A,UTF3
	MOVEM AA,UTF4
	SKIPGE CH,EIDEV
	TYPR2 (SIXBIT /NFO/)
	HRRM CH,UTF
	TDZA E,E
.FILEX:	MOVEI E,1	;DON'T RENAME
	MOVEI CH,EOFCHR
	PUSHJ P,UTYO
	MOVEI CH,14
.FILEA:	PUSHJ P,UTYO
	MOVE CH,UTYOP
	HRRI CH,EOFCHR
	TLNE CH,760000
	JRST .FILEA
	MOVEI CH,UTOBUF-1
	SUB CH,UTYOP
	HRLS CH
	HRRI CH,UTOBUF
	IOT UTYOC,CH
	JUMPN E,.FILE2
	SETZM UTF1
	MOVEI CH,UTYOC
	MOVEM CH,UTF2
	FDELE UTF
	JRST OPNER1
	MOVE A,UTF3
	MOVEM A,UTF1
	MOVE A,UTF4
	MOVEM A,UTF2
.FILE2:	PUSH P,0
	MOVE 0,[REPEAT 5,[EOFCHR_<7*.RPCNT>+]]	;DISGUST
	TSCLOSE UTYOC,
	SETOM EIDEV
	POP P,0
	TLZ FF,UWRITE
	POPJ P,


WWINIT:	PUSHJ P,FFRDEV
WINIT:	MOVSI A,3
	HRR A,UTF
	HRRZM A,EIDEV
	JSP T,WINITA
	JSP TT,OPNER
	POPJ P,

WINITA:	MOVEM A,UTOF
	STOPEN UTYOC,UTOF
	XCT (T)
FHAK:	TLO FF,UWRITE+PNSTOP
	MOVE CH,[700,,UTOBUF-1]
	MOVEM CH,UTYOP
	MOVNI CH,<UTOBE-UTOBUF>*5
	MOVEM CH,UTYOCT
	JRST 1(T)

CNTRLE:	TSCLOSE LPTC,
	TLZ FF,LPTF
	JRST RET

CNTRLB:	TSOPEN LPTC,TSLPTF
]IFE TS,[CNTRLB:]
	MOVEI CH,14
	AOSN CTLBF'
IFE TS-LPTR+1,	PUSHJ P,APILPT
IFN TS,	.IOT LPTC,CH
	TLO FF,PNSTOP+LPTF
	JRST CD

PATCH:	BLOCK 100

IFE TS,[ZZ==.
RDCHN==3î
PCHCHN==UTCCHN"
FLGCHN==6
DATCHN==7
APRCHN==5
TPCHN==2
TPCHNA==40
LPTCHN==PCHCHN
TTYCHN==PCHCHN


LOC 40+APRCHN+APRCHN
JSR IAPRBRK

LOC 40+RDCHN+RDCHN
JSR RBRK

LOC 40+FLGCHN+FLGCHN
JSR RECYC

LOC 40+DATCHN+DATCHN
BLKO DIS,BLKOP
JRST 4,.

LOC 40+PCHCHN+PCHCHN
JSR PBRK

LOC ZZ

RECYC:	0
	EXCH A,BLKOPR
	MOVEM A,BLKOP
	EXCH A,BLKOPR
	CONO DIS,100+FLGCHN*10+DATCHN
	JRST 12,@RECYC
]DISINI:IFN TS,[SKIPE GETTY
	JRST DIS1
]	MOVE T,Z
	IDIVI T,5
	ADDI T,CBUF+2
	MOVEM T,DISBUF
	MOVEI TT,20115+IFN TS,2
	IOR TT,CHSIZ
	PUSH T,TT
	PUSH T,[221700060000
	HRLI T,600
	MOVEM T,DISPNR
	LDB TT,[(40200)CHSIZ
	MOVNS TT
	MOVEI T,170.
	LSH T,(TT)
	TRZ T,7
	MOVEM T,CHCNTS
	MOVNM T,CHCNT
	MOVEI T,100.
	MOVEM T,LINES
IFE TS,	CLEARM BARPNT
	POPJ P,

IFN TS,[
DIS1:	CLEARM CRCNT
	IOT DCHN,[^T]
	POPJ P,

DIS2:	CAIE IN,1(A)
	JRST DIS3	;"[ HA HA
	IOT DCHN,["]]
	IOT DCHN,["[]	;"] HA HA
DIS3:	CAIN CH,15
	AOS CRCNT'
	CAIE CH,^T
	CAIN CH,14
	JRST DTYOTL
	IOT DCHN,CH
	TRO FF,VIEWF
	POPJ P,

DTYOTL:	IOT DCHN,["_]
	ADDI CH,100
	IOT DCHN,CH
	POPJ P,
]

DEFINE LC A
A,,DISLC
TERMIN

DEFINE UC A
DISUC,,A
TERMIN

DEFINE AC A
A,,A
TERMIN


DISAD:
IFN TS,[SKIPE GETTY
	JRST DIS2
]	SKIPN LINES
	POPJ P,
	MOVE A,PT
	CAIN IN,1(A)
	JRST DISBAR
DISA1:	CAIL CH," 
	CAILE CH,"Z
	JRST DISA4
DISA2:	AOSG CHCNT
DISA3:	IDPB CH,DISPNR
	POPJ P,

DISA4:	CAIN CH,^M
	JRST DISACR
	CAIN CH,^J
	JRST DISALF
	CAIN CH,^G
	JRST DISABL
	CAIE CH,^I
	JRST DISCHR
DSATB:	MOVEI CH,40
	PUSHJ P,DISA2
	LDB CH,[300,,CHCNT]
	JUMPN CH,DSATB
	POPJ P,

DISACR:	MOVE CH,CHCNTS
	MOVNM CH,CHCNT
	MOVEI CH,34
	JRST DISA3

DISCHR:	CAIL CH,40
	CAIL CH,140
	JRST DISCTL
	MOVS CH,DTAB-"[(CH)
DISCH2:	HRRI CH,36
	IDPB CH,DISPNR
	MOVSS CH
	IDPB CH,DISPNR
	MOVEI CH,35
	JRST DISA2

DISALF:	SOS LINES
	MOVEI CH,33
	JRST DISA3

DISABL:	MOVSI CH,63
	JRST DISCH2

DISBAR:IFE TS,[	MOVEI A,36
	IDPB A,DISPNR
	MOVEI A,62
	IDPB A,DISPNR
	MOVE A,DISPNR
	MOVEM A,BARPNT
]IFN TS,[	MOVEI A,36
	IDPB A,DISPNR
	MOVEI A,61
	IDPB A,DISPNR
	MOVEI A,72
	IDPB A,DISPNR
	MOVEI A,62
	IDPB A,DISPNR
	MOVEI A,72
	IDPB A,DISPNR
	MOVEI A,60
	IDPB A,DISPNR
]	MOVEI A,35
	IDPB A,DISPNR
	JRST DISA1

DTAB:	53 ? 52 ? 54 ? 46 ? 51 ? 50
LINES:	0

DISCTL:	PUSH P,CH
	MOVEI CH,36
	IDPB CH,DISPNR
	MOVEI CH,50
	IDPB CH,DISPNR
	MOVEI CH,73
	IDPB 16,DISPNR
	MOVEI CH,35
	IDPB 16,DISPNR
	MOVE CH,(P)
	CAIGE CH,100
	JRST DISCT1
	MOVEI 16,61
	PUSHJ P,DISA2
DISCT1:	MOVE CH,(P)
	ANDI CH,70
	JUMPE CH,DISCT2
	LSH CH,-3
	ADDI CH,"0
	PUSHJ P,DISA2
DISCT2:	POP P,CH
	ANDI CH,7
	ADDI CH,"0
	PUSHJ P,DISA2
	MOVSI CH,77
	JRST DISCH2

DISCLG:	IFN TS,[
	SKIPE GETTY
	JRST DISCL1
]	MOVE A,DISPNR
	MOVEI CH,37
	IDPB CH,A
	TLNE A,550000
	JRST .-2
	TLC A,2400
	MOVEI CH,3000
	IDPB CH,A
	HRRZ B,DISBUF
	MOVEM B,BLKOPR
IFN TS,[SUB A,DISBUF
	MOVNI A,4(A)
	HRLM A,BLKOPR
	SKIPGE DSW
	DFLUSH
	SKIPL DSW'
	DSTART BLKOPR
	JFCL
]IFE TS,[MOVEI CH,6
	CAIL B,-20.(A)
	XORM CH,1(B)
	CONO PI,2237
	CONO PI,4002
	CONO APR,2000+APRCHN
]	POPJ P,
IFN TS,[DISCL1:	POPJ P,
]
GET:	MOVE TT,IN
	IDIVI TT,5
	LDB CH,BTAB(TT1)
	POPJ P,

PUT:	MOVE TT,OUT
	IDIVI TT,5
	DPB CH,BTAB(TT1)
	POPJ P,

GETINC:	PUSHJ P,GET
	AOJA IN,GETINC-1

PUTINC:	PUSHJ P,PUT
	AOJA OUT,CPOPJ

BTAB:	350700+TT,,CBUF
	260700+TT,,CBUF
	170700+TT,,CBUF
	100700+TT,,CBUF
	10700+TT,,CBUF


NROOM:	MOVEM 17,AC2+15
	MOVE 17,PT
	CAMN 17,Z
	JRST NROOM1
NROOM7:	MOVE 17,[(2)AC2]
	BLT 17,AC2+14
	JUMPL C,NROOM6
	SETOM GCFLG
	CLEARM CRREL
	CLEARM RREL
NROOM9:	MOVE 17,Z
	ADD 17,C
	CAML 17,MEMSIZ
	JRST GC
	ADDI 17,CBUF*5
	MOVE 14,C
	IDIVI 14,5
	IMULI 15,7
	MOVN 13,15
	MOVEI 15,-43(15)
	MOVE 11,PT
	ADDI 11,CBUF*5
	IDIVI 11,5
	MOVNI 16,-5(12)
	IMULI 16,7
	DPB 16,[(300600)NROOM2]
	ADDI 14,1(11)
	MOVE 16,Z
	ADDI 16,CBUF*5
	IDIVI 16,5
	MOVEI B,1(16)
	SUB B,11
	HRLI 11,(MOVE A,(B))
	HRLOI 12,(ROT A,)
	HRLI 13,(ROTC A,)
	HRLI 14,(MOVEM AA,(B))
	HRLI 15,(ROTC A,)
	MOVE 17,[JRST,.+3]
	MOVE 16,.+1
	SOJGE B,11
	ROTC A,43(13)
	DPB A,NROOM2
NRM5A:	ADDM C,Z
NROOM5:	MOVS 17,[(2)AC2]
	BLT 17,17
	POPJ P,

IFN TS,[
GCRM:	PUSHJ P,COREX
	TYPR2 (SIXBIT /SCE/)
	POPJ P,
]

NROOM2:	(10000),-1(14)

NROOM1:	ADDM C,Z
	MOVE 17,AC2+15
	POPJ P,

NROOM6:	MOVE 14,PT
	ADDI 14,CBUF*5
	IDIVI 14,5
	MOVEM 14,B
	HRRM 14,NROOM4
	IMULI 15,7
	DPB 15,[(300600)NROOM4
	MOVNI 15,-44(15)
	DPB 15,[(360600)NROOM4
	MOVE 11,Z
	ADDI 11,CBUF*5
	IDIVI 11,5
	ADDI 11,1
	MOVE 13,C
	IDIVI 13,5
	ADDI 13,-1(11)
	MOVNM 14,12
	IMULI 12,7
	MOVNI 15,-43(12)
	SUBI B,1(13)

NROOM8:	HRLI 11,(MOVE AA,(B))
	HRLI 12,(ROTC A,)
	HRLI 13,(MOVEM A,(B))
	MOVE 14,[ADDM A,@13
	HRLI 15,(ROTC A,)
	MOVE 17,[JRST NROOM3
	ADDM C,Z
	LDB C,NROOM4
	MOVE A,@11
	ROT A,-1
	MOVE 16,.+1
	AOJLE B,11

NROOM3:	DPB C,NROOM4
	JRST NROOM5

NROOM4:	0

GC:	AOSE GCFLG
IFN TS,	JRST GCRM
IFE TS,	TYPR1 [ASCII /STORAGE CAPACITY EXCEEDED
!/]
GCC:	SETOM GCPTR
	CLEARM SYMS
	MOVE T,[SYMS,,SYMS+1
	BLT T,SYMEND-1	;CLEAR O SYM TAB
	MOVEI T,CPTR	;MARK CPTR
	PUSHJ P,GCMA
	HRRZ T,P
	CAIL T,PDL
	PUSHJ P,GCMA
	CAILE T,PDL
	SOJA T,.-2	;MARK PDL
	HRRZ T,AC2+PF-2	;SAVED PF
	CAIL T,PFL
	PUSHJ P,GCM
	CAILE T,PFL
	SOJA T,.-2
	MOVE T,[(,-NQREG)QTAB]
	PUSHJ P,GCM
	AOBJN T,.-1
	SKIPGE GCPTR
	JRST NROOM9

GCS:	MOVE IN,QRBUF
GCS1A:	MOVSI TT,1*5
	MOVE OUT,GCPTR
GCS1:	HRRZ A,GCTAB(OUT)
	ADD A,QRBUF
	CAMGE A,IN
	JRST GCS2
	CAMGE A,TT
	MOVE TT,A
GCS2:	SOJGE OUT,GCS1
	TRNN TT,-1
	JRST NROOM9
	MOVE F,TT
	IDIVI IN,5
	IDIVI F,5
	ADDI IN,CBUF+1
	ADDI F,CBUF
	MOVS OUT,F
	MOVE T,F
	SUB T,IN
	JUMPLE T,GCS4A
	HRR OUT,IN
	MOVE B,Z
	IDIVI B,5
	SUBI B,-CBUF(T)
	BLT OUT,(B)
	MOVNS OUT,T
	IMULI OUT,5
	ADDM OUT,BEG
	ADDM OUT,PT
	ADDM OUT,Z
	ADDM OUT,RREL
	MOVE CH,GCPTR
GCS3:	HRRZ A,GCTAB(CH)
	ADD A,QRBUF
	CAMGE A,TT
	JRST GCS4
	ADDM OUT,GCTAB(CH)
	HLRZ A,GCTAB(CH)
	CAIN A,CPTR
	ADDM OUT,CRREL
	SKIPL (A)
	ADDM T,(A)
	SKIPGE (A)
	ADDM OUT,(A)
GCS4:	SOJGE CH,GCS3
	ADD TT,OUT
GCS4A:	MOVE IN,TT
	PUSHJ P,GETINC
	CAIE CH,GLITCH
GCERR:	TYPR1 [ASCII /GC ERROR
!/]
	PUSHJ P,GETINC
	MOVE A,CH
	PUSHJ P,GETINC
	ROT CH,7
	IOR A,CH
	ADDI IN,-3(A)
	JRST GCS1A

GCM:	MOVE IN,(T)
	TLZE IN,400000
	TLZE IN,377777
	POPJ P,
	ADD IN,QRBUF
GCM2:	CAML IN,BEG
	POPJ P,
	PUSHJ P,GET
	CAIE CH,GLITCH
	POPJ P,
	SUB IN,QRBUF
	JUMPL IN,CPOPJ
	AOS TT,GCPTR
	CAIL TT,GCTBL
	JRST GCERR
	HRL IN,T
	MOVEM IN,GCTAB(TT)
	POPJ P,

GCMA:	LDB TT,[(221400+T)]	;PTR IN T
	CAIE TT,700
	POPJ P,
	MOVE IN,-1(T)	;GET COMAX
	SUB IN,1(T)	;SUB COMCNT
	LDB TT,[(360600+T)]
	IDIVI TT,7
	MOVNI TT1,-2(TT)
	HRRZ TT,(T)
	IMULI TT,5
	ADDI TT,TT1(CH)
	SUBM TT,IN
	JRST GCM2

IFE TS,[IAPRBRK:	0
	PUSH P,A
	CONSO 1200
	JRST ILMEM
	CONO 1440+APRCHN
	JSR APRBRK"
	SOSG BARFL1
	JRST BARFL2
APRBK7:	SKIPGE OFTAPE
	CONSZ PTR,400
	JRST APRB69
	SETOM PWOWOT
APRB69:	SKIPGE RBRK+1
	AOSG RDRUN
	JRST APRBK3
	MOVEI A,(JSR)
	HRLM A,RBRK+1
	SETOM OFTAPE
	CONO PTR,400
APRBK3:	CONSO DIS,77
	JRST APRBA
	MOVE A,BLKOP
	HRRZS BLKOP
	CAMN A,BLKOP
	CONO PI,4002
APRBA:	AOS A,TIME
	SUB A,TSTPTM
	CAIL A,10
	SKIPG BRKFLG
	JRST APRBA1
	SETOM BRKFLG
	HRROS STOPF
APRBA1:	POP P,A
	JRST 12,@IAPRBRK

BARFL2:	CONSO DIS,77
	JRST APRBK7
	MOVEI A,30.
	MOVEM A,BARFL1
	LDB A,BARPNT
	TRC A,22
	DPB A,BARPNT
	JRST APRBK7


ILMEM:	MOVEI B,0
	CONSO 200000
	MOVEI B,10
	CONO 433550+APRCHN
	MOVE P,[(,-LPDL)PDL]
	MOVEI CH,7
	PUSHJ P,TYO
	SOJG B,.-1
	TRO FF,VIEWF
	JRST 10,ERR+1
IFN LPTR,[
APILPT:	EXCH A,CH
	PUSHJ P,PILPT"
	EXCH A,CH
	POPJ P,
]

PIPUN:	SKIPG PUNCC
	JRST .-1
	IDPB CH,PUNIP
	HRRZ CH,PUNIP
	CAIN CH,PUNBE-1
	MOVEI CH,PUNBO
	HRRM CH,PUNIP
	SOS CH,PUNCC
	CONSO PTP,7
	CONO PTP,10+PCHCHN
	POPJ P,

PBRK:	0
	JSR UTCBRK"
	PUSH P,A
PBRK1:
IFN LPTR,[
	JRST LPTBRK"
LPTRTN":]
	CONSO PTP,10
	JRST PITELE
	MOVEI A,+<PUNBE-PUNBO>*4-4
	CAMG A,PUNCC
	JRST PUNSTP
	ILDB A,PUNOP
	DATAO PTP,A
	HRRZ A,PUNOP
	CAIN A,PUNBE-1
	MOVEI A,PUNBO
	HRRM A,PUNOP
	AOSA PUNCC
PUNSTP:	CONO PTP,0
POPRET":	POP P,A
	JRST 12,@PBRK

PUNCC:	+<PUNBE-PUNBO>*4-4
PUNIP:	(1000)PUNBO-1
PUNOP:	(1000)PUNBO-1
TTYRET=POPRET
]
IFN TS,[
TSINT:	0
	0
	EXCH A,TSINT
	TRNE A,TYPIN
	JRST TSINT1
TSINT2:	TRNE A,IOCERR
	DISMISS [[TYPR2 (SIXBIT \IOC\)]]
	TRNE A,MPV1
	JRST TSINT4
TSINT5:	TRNE A,PDLOV1
	DISMISS [[TYPR1 [ASCII /PUSHDOWN CAPICITY EXCEEDED
!/]]]
	TRNE A,DISMPV
	JRST TSINT6
TSINT7:	MOVE A,TSINT
	DISMISS TSINT+1

TSINT1:	MOVEM A,TSINT3'
	ITYI A,
	JRST TSINT2
	CAIN A,^Z
	.LOGOUT
	CAIE A,^G
	SKIPA A,TSINT3
	JRST .+2
	JRST TSINT2
	RESET TYOC,
	RESET TYIC,
	SETOM STOPF
	HRRZ A,TSINT+1
	CAIN A,TYIW
	DISMISS [GOX]
	MOVE A,TSINT3
	JRST TSINT2

TSINT4:	SOS TSINT+1
	MOVEM A,TSINT3
	LDB A,[330200,,@TSINT+1]
	CAIE A,2
	CAIN A,3
	PUSHJ P,COREX
	DISMISS [[TYPR2 (SIXBIT /MPV/)]]
	MOVE A,TSINT3
	JRST TSINT5

TSINT6:	IITYPR [SIXBIT /DISMPV/]
	.DSTOP
	JRST TSINT7
]IFE TS,[RBRK:	0
	JSR RDTEM
	EXCH B,RDTEM
	PUSH P,A
	MOVNI A,10
	MOVEM A,RDRUN
	IDPB B,RDIP
	HRRZ A,RDIP
	CAIN A,RBEND-1
	MOVEI A,RBUF
	HRRM A,RDIP
	AOS A,RCC
	CAILE A,RDCHAR/4
	CAIE B,214
	CAIL A,RBEND*5-RBUF*5-30
	JRST RBRK2
RBRK3:	POP P,A
	MOVE B,RDTEM
	JRST 12,@RBRK

RBRK2:	MOVEI A,(JSR)
	HRLM A,RBRK+1
	JRST RBRK3

RDTEM:	0
	CONO PTR,RDCHN
	JRST 12,@RBRK

7RI:	TLNE FF,7W	;OR PATCH TO TLZA
	TYPR2 (SIXBIT \7RI\)
7RI2:	MOVEI AA,0BIT+1BIT
	XORM AA,7ST
	TLON FF,7R
	CONO 760,@7ST
	JRST RET

7WI:	TRNE FF,ARG
	JUMPE A,7WI1
	TLNE FF,7R	;OR PATCH TO TLZA
	TYPR2 (SIXBIT \7WI\)
	MOVEI AA,1BIT
	TLON FF,7W
	PUSHJ P,7WCH3A
	JRST RET

7WI1:	MOVEI CH,176
	TLZE FF,7W
	PUSHJ P,7WCH
	JRST RET
7RCH:	PUSH P,AA
	PUSH P,C
	MOVEI CH,0
7RCH1A:	CLEARM 7ERRF
	MOVEI C,200
7RCH1:	LSH C,-1
	JUMPE C,7RCH2
	PUSHJ P,7RCH3
	JUMPL AA,7RCH4
	TRNE AA,1BIT
	TDO CH,C
7RCH1B:	PUSHJ P,7RCH3A
	JRST 7RCH1

7RCH2:	PUSHJ P,7RCH3
	JUMPGE AA,7RCH5
7RCH2B:	PUSHJ P,7RCH3A
	PUSHJ P,7RCH3
	JUMPL AA,7RCH2A
	SKIPE 7ERRF
	TRC AA,0BIT+1BIT
	PUSHJ P,7RCH3A
	SKIPE 7ERRF
	JRST 7RCH1A
	CAIE CH,176
	JRST 7RCH7
	PUSHJ P,7RCH3
	JUMPGE AA,[TYPR2 (SIXBIT \7LZ\)]
	TLZ FF,7R
	MOVEI CH,14
	TRO FF,FINF
	JRST 7RCH7

7RCH4:	MOVEI AA,1BIT
7RCH5:	SETOM 7ERRF
	JRST 7RCH1B

7RCH2A:	SETOM 7ERRF
	MOVEI AA,1BIT
	JRST 7RCH2B


7RCH3:	CONI 760,AA
	ANDI AA,0BIT+1BIT
	XOR AA,7STA
	JUMPE AA,7RCH3
	CONI 760,AA
	ANDI AA,0BIT+1BIT
	XOR AA,7STA
	XORM AA,7STA
	CAIN AA,0BIT+1BIT
	SETOM AA
	POPJ P,

7RCH3A:	JUMPL AA,7RCH3B
	XORM AA,7ST
7RCH3C:	CONO 760,@7ST
	POPJ P,
7RCH3B:	MOVEI AA,0BIT+1BIT
	XORM AA,7ST
	SETOM AA
	JRST 7RCH3C
7WCH:	PUSH P,AA
	PUSH P,C
	PUSH P,B
7WCH1A:	CLEARM 7ERRF
	MOVEI C,200
7WCH1:	LSH C,-1
	JUMPE C,7WCH2
	MOVEI AA,0BIT
	TDNE CH,C
	MOVEI AA,1BIT
	PUSHJ P,7WCH3A
	JRST 7WCH1

7WCH2:	MOVEI AA,0BIT+1BIT
	PUSHJ P,7WCH3A
	MOVEI AA,0BIT
	SKIPE 7ERRF
	MOVEI AA,1BIT
	PUSHJ P,7WCH3A
	SKIPE 7ERRF
	JRST 7WCH1A
	POP P,B
7RCH7:	POP P,C
	POP P,AA
	POPJ P,

7WCH3A:	XORM AA,7ST
	CONO 760,@7ST
	HRRM AA,7WCH3B
7WCH3:	CONI 760,AA
	ANDI AA,0BIT+1BIT
	CAMN AA,7STA
	JRST 7WCH3
	CONI 760,AA
	ANDI AA,0BIT+1BIT
	XOR AA,7STA
	XORM AA,7STA
7WCH3B:	CAIE AA,.
	SETOM 7ERRF
	POPJ P,

7ST:	0
7STA:	0
7ERRF:	0
PIRPA3:	AOSN PWOWOT
	PUSHJ P,PIBUF1
	AOSG OFTAPE
	JRST PIBUFX
	MOVEI CH,20
	SKIPL RBRK+1
	PUSHJ P,RDST+1
PIRPA:	SKIPG RCC
	JRST PIRPA3
	HRRZ CH,RDOP
	CAIN CH,RBEND-1
	MOVEI CH,RBUF
	HRRM CH,RDOP
	SOS CH,RCC
	SKIPL OFTAPE
	CAIL CH,RDCHAR*3/4
	JRST PIRPA4
	SKIPL RBRK+1
	PUSHJ P,RDST
PIRPA4:	ILDB CH,RDOP
	CAIN CH,177
	JRST PIRPA
	JUMPE CH,PIRPA
	POPJ P,

PIBUFX:	CONO PTR,400
	JRST PIBUFD

PIBUF1:	CLEARM OFTAPE
PIBUFC:	CONO PTR,0
PIBUFD:	MOVNI CH,10
	MOVEM CH,RCC
	MOVE CH,RDIP
	MOVEM CH,RDOP
	MOVEI CH,(JSR)
	HRLM CH,RBRK+1
	MOVEI CH,14
	POPJ P,

RDST:	MOVEI CH,10
	MOVNM CH,RDRUN
	CONSZ PTR,20
	JRST .-1
	HRLI CH,(DATAI PTR,)
	HLLM CH,RBRK+1
	CONO PTR,RDCHN(CH)
	POPJ P,

RDOP:	(700)RBUF
RDIP:	(700)RBUF
RCC:	0
OFTAPE:	0
RDRUN:	0
]
LOOK:	HRLZI A,-27
	SKIPA TT,UFPNTR"
	ADDI TT,2
	CAMN B,(TT)
	CAME C,1(TT)
	AOBJN A,LOOK+2
	TLZN A,-1
	POPJ P,	;NOT FOUND
	AOJA A,WR2
POPJ1:
WR2:	AOS (P)
IFN TS,CBELLS:	POPJ P,10
IFE TS,	POPJ P,


	SKIPA A,AA
FRD:	MOVSI A,(SIXBIT /@/)
	MOVSI AA,(SIXBIT /@/)
	MOVE T,[(600)A
FFRD1A:	PUSHJ P,RCH
	CAIN CH,ALTMODE
	POPJ P,
	TRC CH,40
	JUMPE CH,FRD-1
	CAIN CH,77
	MOVEI CH,0
	CAME T,[(600)AA
	IDPB CH,T
	JRST FFRD1A

IFN TS,[
FFRRDD:	PUSHJ P,FRD
	MOVEM A,UTF1
	MOVEM AA, UTF2
	POPJ P,

FFRDEV:	PUSHJ P,FRD
	JRST FRDEV2
	SKIPE A,RDFILE
	HLRM A,UTF
	JRST FFRDEV

FRDEV2:	SKIPE A,RDFILE
	HLRM A,UTF
	POPJ P,

FFRD:	MOVE T,[440600,,RDFILE']
	SETZM RDFILE
FFRD0:	PUSHJ P,LRCH
	CAIE CH," 	;SPACE
	CAIN CH,"		;TAB
	JRST FFRD0
FFRD1:	CAIE CH," 	;SPACE
	CAIN CH,"		;TAB
	JRST POPJ1
	CAIN CH,ALTMOD
	POPJ P,
	CAIN CH,":
	JRST FFRD2
	CAIN CH,";
	JRST FFRD3
	CAIN CH,^A
	JRST FFRD4
	CAIN CH,^B
	JRST FFRD5
	CAIN CH,^Q
	PUSHJ P,LRCH
	SUBI CH,40
	TLNE T,770000
	IDPB CH,T
	PUSHJ P,LRCH
	JRST FFRD1

FFRD2:	SKIPE T,RDFILE
	HLRM T,UTF
	JRST FFRD

FFRD3:	SKIPE T,RDFILE
	WSNAME T,
	JRST FFRD
FFRD4:	SKIPA T,UTF1
FFRD5:	MOVE T,UTF2
	MOVEM T,RDFILE
	JRST POPJ1
]
IFE TS,[RRED:	MOVE A,LFRED1
	MOVE AA,LFRED2
	PUSHJ P,OPNRD
	TYPR1 [ASCII /FILE NOT FOUND
!/]
	POPJ P,


.OPNRD:	PUSHJ P,FRD
	MOVEM A,LFRED1
	MOVEM AA,LFRED2
.OPNR1:	PUSHJ P,OPNRD"
	TYPR1 [ASCII /FILE NOT FOUND
!/]
	TLO FF,UREAD
	JRST CNTRU1]

IFN TS,[
TSLPTF:	1,,(SIXBIT \LPT\)
	SIXBIT \.TECO.\
	SIXBIT \LPTO\
LPTOF:	3,,(SIXBIT /LPT/)
	SIXBIT /.TECO./
	SIXBIT/WPAPER/
FDF:	0
	SIXBIT \.FILE.\
	SIXBIT \(DIR)\
UTOF:	0
	SIXBIT \.TECO.\
	SIXBIT \OUTPUT\
OTTY:	1,,(SIXBIT \TTY\)
	SIXBIT \.TECO.\
	SIXBIT \TYO\
ITTY:	(SIXBIT \TTY\)
	SIXBIT \.TECO.\
	SIXBIT \TYI\
UTF:	3,,(SIXBIT \UT2\)
]
UTF1:LFRED1:	0
UTF2:LFRED2:	0
IFN TS,[
UTF3:	SIXBIT /@/
UTF4:	SIXBIT /@/
]
IFN TS,[TAPKIL:	HRRZ A,UTF
	CAMN A,ERDEV
	PUSHJ P,UICLS
	CAMN A,EIDEV
	PUSHJ P,UCLOSE
	LDB B,[400,,UTF]
	TLZ FF,CTLUF
	UFLAP B,
	TYPR2 (SIXBIT \UNF\)
	POPJ P,]

IFE TS,[
DELE:	PUSHJ P,FRD
	PUSHJ P,UDELE"
	JRST CNTRU1
]
IFN TS,[
DELE:	PUSHJ P,FFRRDD
	SETZM UTF3
DELRNM:	FDELE UTF
	TYPR2 (SIXBIT \FNF\)
	POPJ P,]

IFE TS,[
.FILE:	PUSHJ P,FRD
	MOVEM A,LFRED1
	MOVEM AA,LFRED2
	PUSHJ P,FILE"
TPFUL:	TYPR1 [ASCII /TAPE FULL!/]
	JRST CNTRU1

CLSTP:	TRNN FF,ARG2
	JRST UCLSTP"
	PUSH P,UFPNTR+2
	PUSH P,UFPNTR
	MOVE A,C
	PUSHJ P,.OPN1
	POP P,UFPNTR
	PUSHJ P,UCLSTP
	POP P,A
	PUSHJ P,FILEST
	JRST UTERR
	JRST CNTRU1

MTNAM:	MOVE OUT,[220600+IN,,177]
	MOVE IN,UFPNTR
	HLLZS 177(IN)

MTN1:	PUSHJ P,RCH
	SUBI CH,40
	JUMPE CH,MTN1
	CAIE CH,175-40
	TLNN OUT,770000
	JRST CNTRU1
	IDPB CH,OUT
	JRST MTN1
]
IFN TS,[
LISTZ:	PUSHJ P,FFRDEV
	JRST LISTFM

LISTY:	PUSHJ P,FFRDEV
LISTF:	SKIPE GETTY
	IOT DCHN,[14]
	SKIPA OUT,[DIS3]
LISTFM:	MOVEI OUT,TYOM
	HRRM OUT,LISTF5
	PUSHJ P,AOFDIR
LISTF2:	IOT FDRC,CH
	CAIE CH,EOFCHR
	CAIN CH,14
	JRST LISTF%
	PUSHJ P,@LISTF5
	JRST LISTF2

LISTF%:	TSCLOSE FDRC,
	POPJ P,]


IFE TS,[
LISTFM:	SKIPA OUT,CTYOM
LISTF:	MOVEI OUT,TYO
	CLEARM LISFLG
	HRRM OUT,LISTF5
]CNTRU5:	PUSHJ P,PTLAB
IFN TS,	PUSHJ P,CRR1
LSTF1A:IFE TS,[SKIPN LISFLG
	JRST LISTF9
	MOVE C,[SIXBIT \TAPE\]
	MOVEI IN,5
	PUSHJ P,TYPR+1
	MOVE B,UFPNTR+2
	PUSHJ P,DPT
	PUSHJ P,LISTF4
LISTF9:]IFN TS,[
	SETOM LISFLG
	MOVEI IN,6
	HRLZ C,UTF
	PUSHJ P,TYPR+1
]	MOVEI B,0
	MOVE A,UFPNTR
	HRLI A,-23.
	SKIPN (A)
	SKIPE 1(A)
	AOJA A,.+2
	AOJA B,.-1
	AOBJN A,.-4
	JUMPN B,.+4
	MOVSI C,(SIXBIT \NO\)
	MOVEI IN,3
	PUSHJ P,TYPR+1
	MOVE OUT,[(600)[SIXBIT /FREE FILES/]-1]
	PUSHJ P,LISTFA
	JUMPE B,.+3
	PUSHJ P,DPT
	PUSHJ P,LISTF4
	MOVE OUT,[(600) [SIXBIT /FREE BLOCKS/]-1]
	PUSHJ P,LISTFA
	HRLZI TT1,-30
	MOVEI A,0
	MOVE D,UFPNTR
	SUBI D,2
	JRST LISTF1



	ADDI D,2
	SKIPN C,(D)
LISTF3:	AOBJN TT1,.-2
	TLNN TT1,-1
	POPJ P,
	HRRZ B,TT1
	SKIPN LISFLG
	JRST .+4
	PUSHJ P,DPT
	MOVEI CH,40
	PUSHJ P,@LISTF5
	ADD TT1,UFPNTR
	MOVE B,55(TT1)
	LDB CH,[(100)104(TT1)]
	DPB B,[(10100)CH]
	SUB TT1,UFPNTR
	ADDI CH,40
	PUSHJ P,@LISTF5
	PUSHJ P,LISTF4
	TLNN C,770000
	PUSHJ P,LISTF4
	TLNE C,770000
	PUSHJ P,TYPR-1
	MOVE C,1(D)
	PUSHJ P,TYPR-1
	HRRZ A,TT1
LISTF1:	SETZB B,E
	MOVE OUT,UFPNTR+1
	ILDB CH,OUT
	CAIN CH,(A)
	AOS E
	CAIE CH,37
	JRST .-4
	SKIPE C,A
	PUSHJ P,LOOK
	SKIPA B,E
	JRST LISTF1+1
	PUSHJ P,DPT
	MOVNS LISFLG
	SKIPGE LISFLG
	PUSHJ P,LISTF4
	SKIPGE LISFLG
	PUSHJ P,LISTF4
	SKIPL LISFLG
	PUSHJ P,CRR1
	JRST LISTF3
	PUSH P,TYPR2A
TYPR:	MOVEI IN,6
TYPR3:	MOVE OUT,[(600)C-1]
	ILDB CH,OUT
	ADDI CH,40
LISTF5:	PUSHJ P,.
	SOJG IN,.-3
TYPR2A:	POPJ P,LISTF4
LISTFA:	MOVEI IN,12.
	PUSHJ P,TYPR+2
SLTAB:
LISTF4:	MOVEI CH,11
	JRST @LISTF5

LISFLG:	0
IFE TS,[
RENAM:	PUSH P,C
	PUSHJ P,FRD
	PUSHJ P,.LOOK"
	TYPR1 [ASCII /NOT FOUND
!/]
	PUSH P,A+2	;FILE NO
	PUSH P,A+3	;PNTR TO FIRST WD OF NAME
	PUSHJ P,FRD
	PUSHJ P,.LOOK
	JRST RNAM1
	CAME A+3,(P)
	TYPR1 [ASCII /ALREADY EXISTS
!/]
RNAM1:	POP P,TT
	MOVEM A,(TT)
	MOVEM A+1,1(TT)
	POP P,A
	POP P,C
	TRNN FF,ARG2
	POPJ P,
	ADD A,UFPNTR"
	DPB C,[(100)104(A)]
	LSH C,-1
	DPB C,[(100)55(A)]
CTYOM:	POPJ P,TYOM

WINIT:	TLO FF,PNSTOP+UWRITE
	PUSHJ P,OPNWR"
	TYPR1 [ASCII \NO FREE FILES
!\]
	JRST CNTRU1
]

IFN TS,[
RENAM:	PUSHJ P,FFRRDD
	PUSHJ P,FRD
	MOVEM A,UTF3
	MOVEM AA,UTF4
	JRST DELRNM

SIXNTY:	PUSH P,C
	MOVE C,[440600,,(P)]
SIXNT1:	ILDB CH,C
	JUMPE CH,POPAJ
	ADDI CH,40
	PUSHJ P,@LISTF5
	JRST SIXNT1
]

ETYPER:	0
	LDB B,[331100,,40]
	CAIN B,TYPR1_-27.
	JRST ETYP1A
	CAIN B,TYPR2_-27.
	JRST ETYP2A
	CAIN B,ITYPR_-27.
	JRST ETYP3
	MOVEI B,4
	MOVEI CH,^G
	PUSHJ P,TYO
	SOJG B,.-1
	JRST GOX

ETYP1A:	HRRZ B,40
	HRLI B,440700
	ILDB CH,B
	CAIN CH,"!
	JRST ETYP1
	PUSHJ P,TYO
	JRST .-4

ETYP2:	MOVEI CH,TYO
	HRRM CH,LISTF5
	HRLZ C,40
	MOVEI IN,3
	PUSHJ P,TYPR+1
	LDB CH,[270400,,40]
	POPJ P,	;CHANGE TO JUMPE CH,CPOPJ FOR DIGIT ON ERROR COMMENT
	ADDI CH,"0
	PUSHJ P,TYO
	POPJ P,

IFN TS,[
OPNER1:	MOVEI TT,FHAK
OPNER:	LDB A,[270400+TT,,-2]
	DPB A,[270400,,.+1]
	STATUS .,T
	HRLZ D,@-2(TT)
	IITYPR D
	IITYPR [SIXBIT /:/]
	LDB T,[221000,,T]
	CAIL T,LOMEST-1
	MOVEI T,LOMEST-1
	SKIPL A,OMEST(T)
	JRST OPNER2
	RSNAME D,
	IITYPR D
	IITYPR [SIXBIT /;/]
OPNER2:	TLNE A,200000
	TLZ F,CTLUF
	PUSHJ P,CRR
	TYPR1 @OMEST(T)

]
ETYP1:IFE TS,PUSHJ P,IOWAIT
	JRST IFE TS,[GOZ
]IFN TS,[GOX

OMEST:	[ASCII /ISE0!/]	;4.9=>TYPE SYSTEM NAME
	200000,,[ASCII /NO SUCH DEVICE!/]	;4.8=>DON'T TRY TO ^U
	[ASCII /WRONG DIRECTION!/]
	[ASCII /TOO MANY TRANSLATIONS!/]
	SETZ [ASCII /FILE NOT FOUND!/]
	[ASCII /DIRECTORY FULL!/]
	[ASCII /DEVICE FULL!/]
	200000,,[ASCII /DEVICE NOT READY!/]
	200000,,[ASCII /DEVICE NOT AVAILABLE!/]
	SETZ [ASCII /ILLEGAL FILE NAME!/]
	[ASCII /MODE NOT AVAILABLE!/]
	[ASCII /ISE11!/]

LOMEST==.-OMEST
]

ETYP2A:	PUSHJ P,ETYP2
	JRST ERRP1

ETYP3:	PUSHJ P,ETYP2
	PUSHJ P,CRR
	JRST 2,@ETYPER


IFE TS,[
CNTRU1:	MOVE A,COMCNT
	CAILE A,3
	POPJ P,
	HRRZ A,P
	CAILE A,PDL+2
	POPJ P,
CNTRLU:	TRO FF,VIEWF
	PUSHJ P,.OPNTP
	SETOB IN,LISFLG	;MAKE SURE DISAD TEST FOR DISBAR FAILS
]

IFN TS,[CNTRLV:	PUSH P,CPOPJ
	SKIPA
CNTRLU:	PUSHJ P,GDEV
	SKIPE GETTY
	JRST LISTF
	TRO FF,VIEWF
	PUSHJ P,IOFDIR
	MOVE C,[-DIRL,,BFDR]
	IOT FDRC,C
	TSCLOSE FDRC,
	SETOM IN
]	MOVEI A,40
	HRRM A,DPT5
	PUSH P,CHSIZ
	MOVEI A,20
	MOVEM A,CHSIZ
	MOVEI A,DISAD
	HRRM A,LISTF5
	PUSHJ P,DISINI
IFN TS,[MOVE A,UTF
	TRC A,(SIXBIT /DD /)
	TRNN A,777700
	JRST CTLUD
	TRC A,+(SIXBIT /DD /)#(SIXBIT /UT /)
	TRNE A,777700
	JRST CTLUX
]	MOVE OUT,UFPNTR"+1
CNTRU4:	MOVEI C,25.
	MOVEI T,0
CNTRU2:	ILDB B,OUT
	CAIN B,37
	JRST CNTRU3
	JUMPE B,CNTRU6
	PUSHJ P,CNTRUA
CNTR6A:	SOJG C,CNTRU2
	PUSHJ P,CRR1
	JRST CNTRU4


CNTRU6:	ADDI T,3
	JRST CNTR6A

CNTRU3:	PUSHJ P,CNTRU5
CTLUX:	PUSHJ P,DISCLG
	POP P,CHSIZ
	POPJ P,

CNTRUA:	JUMPE T,SLDPT
	PUSHJ P,SPSP
	SOJA T,.-2


IFN TS,[
;DISK DIRECTORY FORMAT

NDV==4
NDLTA==129.
NDEBM==1
NDFLS==128.*5
DIRL=NDV+NDLTA+NDEBM+NDFLS

UFPNTR:	BFDR
	500,,BFDR+55
BFDR:	BLOCK DIRL	;USED BY ^H

CTLUD:	LDB C,[2200,,UTF]
	LSH C,18.
	PUSHJ P,TYPR
	MOVEI D,0
CTLUD2:	MOVE B,D
	PUSHJ P,4RDPT
	AOS D
	CAIE D,15.
	JRST CTLUD2
	PUSHJ P,CRR1
	PUSHJ P,CRR1
	MOVEI D,0
	MOVEI C,0
CTLUD3:	MOVE B,D
	IMULI B,15.
	PUSHJ P,4RDPT
	PUSHJ P,SPSP
	PUSHJ P,SPSP
	MOVSI E,-15.
CTLUD4:	HRRE B,BFDR+NDV(C)
	PUSHJ P,4RDPT
	AOS C
	CAIN C,128.
	JRST CTLUD5
	AOBJN E,CTLUD4
	PUSHJ P,CRR1
	AOJA D,CTLUD3

CTLUD5:	PUSHJ P,CRR1
	PUSHJ P,PTLAB
	MOVE B,BFDR+2
	PUSHJ P,4RDPT
	PUSHJ P,SPSP
	MOVE OUT,[(600)[SIXBIT /FREE TRACKS/]-1]
	PUSHJ P,LISTFA
	PUSHJ P,CRR1
	MOVSI D,-128.
CTLUD6:	HRRZ E,D
	IMULI E,5
	SKIPE BFDR+NDV+NDLTA+NDEBM(E)
	PUSHJ P,CTLUD7
	AOBJN D,CTLUD6
	JRST CTLUX


CTLUD7:	HRRZ B,D
	MOVEI TT,3
	PUSHJ P,NRDPT
	HLRZ C,BFDR+NDV+NDLTA+NDEBM+3(E)
	MOVEI CH,40
	SKIPE C
	MOVEI CH,"*
	PUSHJ P,@LISTF5
	MOVE C,BFDR+NDV+NDLTA+NDEBM(E)
	PUSHJ P,TYPR
	PUSHJ P,SPSP
	MOVE C,BFDR+NDV+NDLTA+NDEBM+1(E)
	PUSHJ P,TYPR
	PUSHJ P,SPSP
	MOVE C,BFDR+NDV+NDLTA+NDEBM+2(E)
	PUSHJ P,TYPR
	PUSHJ P,SPSP
	MOVSI C,-128.
	HRRE E,BFDR+NDV+NDLTA+NDEBM+3(E)
CTLUD8:	MOVE B,E
	PUSHJ P,4RDPT
	SKIPG E
	JRST CRR1
	HRRE E,BFDR+NDV(E)
	HRRZ TT,C
	CAIE TT,12.
	JRST CTLUD9
	HLLOS C
	PUSHJ P,CRR1
	MOVEI TT,25.
	PUSHJ P,SPSP
	SOJG TT,.-1
CTLUD9:	AOBJN C,CTLUD8
	JRST CRR1

4RDPT:	MOVEI TT,4
NRDPT:	PUSH P,E
	PUSHJ P,RDPT
	POP P,E
	POPJ P,
]

LIGHTS:	TRNE FF,ARG
	JRST LIGT2
IFE TS,	CONI 760,A
IFN TS,	RD760 A,
	LSH A,-9.
	JRST VALRET
LIGT2:	LSH B,9

LIGT1:
IFE TS,	CONO 760,(B)
IFN TS,	WR760 B,
	JRST RET

IFE TS,BRKFLG:	0
SFINDF:	0
	0
BEG:	0
PT:	0
Z:	0
QRBUF:	0
LEV:	0
COMAX:	0
CPTR:	0
COMCNT:	0
NUM:	0
SYL:	0
OSYL:	0
SARG:	0
LIFF:	0
ERR1:	0
ERR2:	0
IFE TS,BLKOP:	0
BLKOPR:	0
DISPNR:	0
CBUFH:	0
IFE TS,[
BARFL1:	0
TIME:	0
TABCNR:	0
]
BARPNT:	0
IFE TS,PWOWOT:	0
CHCNT:	0
DISBUF:	0
CHCNTS:	0
ITERCT:	0
MEMSIZ:	IFE TS,[IFLE MOBY-170000,[<37000-CBUF+MOBY>*5
]IFG MOBY-170000,[777700
]]IFN TS,<2000*N1KB-CBUF>*5
GCPTR:	0
CRREL:	0
GCFLG:	0
RREL:	0
INTDPH:	0
IFN TS,[
SYMLPT:	LDB A,[260100,,FF]	;LPTF
	TLZ FF,LPTF
	DPB A,[400100,,LPTSAV]
	AOSA SYMLF
]
SYMLST:	TRNE FF,ARG
	JRST BEAST
	SETOM SLN
SYML1:	MOVE CH,Z
	IDIVI CH,5
	ADDI CH,CBUF+10
	IDIVI CH,3	;YES.  WELL, LIE DOWN ON THE COUCH
	IMULI CH,3	;THERE AND WE'LL TALK ABOUT IT.
	MOVEM CH,STPNR
	MOVEM CH,STB
	CLEARM PASS
	MOVEI CH,60
	HRRM CH,DPT5
	PUSHJ P,RRED
NPASS:	MOVEM A,STM	;NO EFFECT ON PASS 1
	MOVSI T,1
MNLP2:	MOVE B,[440700,,LBF]
	MOVEM B,LBFIP
	SETOM SLCHS
	CLEARM LSYL
MNLP5:	TLZ FF,DEFF
MNLP:	PUSHJ P,GETSYL
	SKIPGE IFE TS,[BRKFLG] IFN TS,[STOPF]
	JRST GOX
MNLP3:	CAIN CH,";
	JRST SLRCR
	TRNN FF,LET
	JRST MNLP1
	CAIE CH,"=
	CAIN CH,":
	JRST DEFSM
	TLNN FF,DEFF
	TLNE FF,VARF
	JRST DEFSM
	TLZE FF,GLOF
	JRST DEFGLO
	CAMN SYM,[SIXBIT \DEFINE\]
	TLOA FF,DEFF
	JRST MNLP4

MNLP1:	CAIN CH,14
	JRST SLFF
	CAIE CH,13
	CAIN CH,12
	JRST SLLF
	JRST MNLP
MNLP4:	SKIPN PASS
	JRST MNLP

LOGES:	MOVEI D,0
	MOVE A,STB
	MOVE B,STPNR
	SKIPL SYM
	SKIPA B,STM	;LOOK IN LOWER HALF
	MOVE A,STM	;LOOK IN UPPER HALF
LOGES1:	MOVE C,A
	ADD C,B
	IDIVI C,6
	IMULI C,3	;YES, REALLY
	CAMN SYM,(C)
	JRST LOGES2	;FOUND
	CAIL A,-3(B)
	JRST MNLP1	;NOT FOUND
	CAML SYM,(C)
	SKIPA A,C	;LOOK HIGHER
	MOVE B,C	;LOOK LOWER
	JRST LOGES1

LOGES2:	MOVMS D,1(C)
	CAIE CH,")
	MOVEM D,LSYL
	JRST MNLP1

SLRCR:	PUSHJ P,SLRCH
	CAIE CH,^M
	JRST .-2
	JRST MNLP

SLRCH:	PUSHJ P,UTYI
	TRZE FF,FINF
	JRST NP1
	CAIE CH,15
	SKIPN PASS
	POPJ P,
	IDPB CH,LBFIP
	AOS SLCHS
	POPJ P,

SLFF:	SKIPN SLCHS
	TRZA FF,ARG
	PUSHJ P,SLLF4
	SKIPE PASS
	PUSHJ P,FORMF
	HLLOS T
	AOJA T,MNLP2

SLLF:	PUSHJ P,SLLF4
	TRO FF,ARG
	AOJA T,MNLP2

IFN TS,	SYMLF:	0

GETSYL:	MOVE B,[440600,,SYM]
	MOVEI SYM,0
	TDZ FF,[GLOF+VARF,,LET]
GETS6:	PUSHJ P,SLRCH
GETS1:	CAIE CH,"@
	CAIN CH,"!
	JRST GETS6
	CAIN CH,""
	JRST GETSGL
	CAIN CH,"'
	JRST GETSVR
	CAIG CH,"Z
	CAIGE CH,"A
	JRST GETS3
GETS4:	TRO FF,LET
GETS2:	TRC CH,40
	TLNE B,770000
	IDPB CH,B
	JRST GETS6

GETSGL:	TLOA FF,GLOF
GETSVR:	TLO FF,VARF
	TRNE FF,LET
	JRST GETS6
	PUSHJ P,SLRCH
	JRST GETSYL

NP1:	SUB P,[2,,2]
	SKIPE PASS
	JRST SYME
PASS2:	SETOM PASS
	PUSHJ P,RRED
IFN TS,[
LPTW2:	.OPEN UTYOC,LPTOF
	JRST LPTW1
	JSP T,FHAK	;INITIALIZE PRINTER
	JFCL
]
LPTW3:	PUSHJ P,.FNPNT
	CLEARM SLCHS
	MOVE A,STB
	MOVE B,STPNR
	PUSHJ P,SORT
	MOVE A,STB
PASS21:	CAMGE A,STPNR
	SKIPGE (A)
	JRST NPASS
	ADDI A,3
	JRST PASS21

GETS3:	CAIG CH,"9
	CAIGE CH,"0
	CAIN CH,".
	JRST GETS2
	CAIE CH,"%
	CAIN CH,"$
	JRST GETS4
	POPJ P,

IFN TS,[
LPTW1:	SKIPGE STOPF
	JRST GOX
	MOVEI A,30.
	SLEEP A,
	JRST LPTW2
]
SLLF4:	SKIPN PASS
	POPJ P,
	TRZE FF,ARG
	PUSHJ P,CRR1
	MOVEI B,1(T)
	PUSHJ P,SLDPT
SLLF1C:	HLLZS SYLCPS
	PUSHJ P,SLTAB
	MOVE D,LSYL
	JUMPE D,SLLF1
	HLRZ B,D
	PUSHJ P,SLDPT
	PUSHJ P,SPSP
	MOVEI B,1(D)
	PUSHJ P,SLDPT

SLLF1:	PUSHJ P,SLTAB
	SOSGE SLCHS
	JRST SLLF2
	MOVE B,[440700,,LBF]
	MOVEM B,LBFIP
SLLF1B:	ILDB CH,LBFIP
	PUSHJ P,PPA
	AOS AA,SYLCPS
	CAIE CH,11
	JRST SLLF1A
	ADDI AA,7
	TRZ AA,7
	HRRM AA,SYLCPS
SLLF1A:	SOSL SLCHS
	JRST SLLF1B
SLLF2:	HRRZ B,T
	IDIVI B,60.
	JUMPN E,CPOPJ
	PUSH P,B
	MOVEI AA,69.
SYLCPS:	SUBI AA,.
	PUSHJ P,SPSP
	SOJG AA,.-1
	MOVE C,[SIXBIT \ PAGE \]
	PUSHJ P,TYPR
	HLRZ B,T
	PUSHJ P,DPT
	POP P,B
	JUMPE B,CPOPJ
	MOVEI CH,".
	PUSHJ P,PPA
	JRST DPT

DEFSM:	SKIPE PASS
	JRST MNLP
	PUSHJ P,ES
	JUMPN D,MNLP5
	MOVNM T,1(F)
	MOVEI D,(SIXBIT \V\)
	TLNE FF,DEFF
	MOVEI D,(SIXBIT \M\)
	TLZE FF,VARF+DEFF
	MOVEM D,2(F)
	TLZN FF,GLOF
	JRST MNLP
DEFGLO:	SKIPE PASS
	JRST MNLP
	PUSHJ P,ES
	MOVEI D,(SIXBIT \G\)
	MOVEM D,2(F)
	JRST MNLP

ES:	MOVEI D,0
	MOVE F,STB
AES2:	CAML F,STPNR
	JRST AES1
	CAMN SYM,(F)
	JRST AES1A
	ADDI F,3
	JRST AES2
AES1:	MOVEM SYM,(F)
	CLEARM 1(F)
	CLEARM 2(F)
	ADDI F,3
	EXCH F,STPNR
	POPJ P,
AES1A:	MOVMS D,1(F)
	POPJ P,

LBFIP:	0
STB:	0
STM:	0
STPNR:	0
PASS:	0
SLN:	0
LSYL:	0
SLCHS:	0
SYME:	MOVE A,STB
	MOVE B,STPNR
	CAMN A,B
	JRST SLEND
SLPP:	MOVE B,STPNR
	SUBI B,-3(A)
	MOVE AA,[747474747474]
	CAIL B,3*6*60.+3
	JRST SLPP2
	IDIVI B,3*6
	MOVEI AA,1(B)
	IMULI AA,10101
	IMULI B,10101
	HRL AA,AA
	HRL B,B
	ASH E,1
	MOVNI E,-6*6(E)
	ROTC AA,6(E)
SLPP2:	MOVEI F,60.
	MOVEM A,SLCHS
SLPL:	MOVE T,[440600,,AA]
	MOVEI D,6
SLPL1:	SKIPL 2(A)
	CAML A,STPNR
	JRST SLEND
	MOVE C,(A)
	PUSHJ P,TYPR
	PUSHJ P,SPSP
	HRLZ C,2(A)
	MOVEI IN,2
	PUSHJ P,TYPR+1
	HRROS 2(A)
	SKIPN 1(A)
	JRST SLPL2
	SKIPG 1(A)
	TRO FF,SYLF
	MOVMS C,1(A)
	HLRZ B,C
	PUSHJ P,SLDPT
	MOVEI CH,40
	TRZE FF,SYLF
	MOVEI CH,"*
	PUSHJ P,PPA
	MOVEI B,1(C)
	PUSHJ P,SLDPT
SLPL3:	REPEAT 3,PUSHJ P,SPSP
	ILDB B,T
	IMULI B,3
	ADD A,B
	SOJG D,SLPL1
	MOVE A,SLCHS
	ADDI A,3
	MOVEM A,SLCHS
	PUSHJ P,CRR1
	SOJG F,SLPL
	ADDI A,3*5*60.
	JRST SLPP

SLPL2:	MOVE C,[SIXBIT \ UNDEF\]
	PUSHJ P,TYPR
	PUSHJ P,SPSP
	JRST SLPL3

SLEND:
SLEND1:	PUSHJ P,FORMF
SLEND2:	SKIPGE STOPF
	JRST GOX
	SOSG SLN
	JRST RET
	MOVEI A,STB
SLEND3:	CAML A,STPNR
	JRST PASS2
	HRRZS 2(A)
	ADDI A,3
	JRST SLEND3
IFN TS,[
LPTSAV:	TLO\TLZ FF,LPTF
	PUSHJ P,.FILEX
	JRST RET
]

SORT:	MOVSI C,400000
SORT1:	HRLM B,(P)
	CAIL A,-3(B)
	JRST SORT7
	PUSH P,A
SORT3:	TDNN C,(A)
	JRST SORT4
	SUBI B,3
	TDNE C,(B)
	JRST SORT2
	EXCH C,(A)
	EXCH C,(B)
	EXCH C,(A)
	EXCH C,1(A)
	EXCH C,1(B)
	EXCH C,1(A)
	EXCH C,2(A)
	EXCH C,2(B)
	EXCH C,2(A)
SORT4:	ADDI A,3
SORT2:	CAME A,B
	JRST SORT3
	ROT C,-1
	POP P,A
	JUMPL C,SORT6
	PUSHJ P,SORT1
	HLRZ B,(P)
	PUSHJ P,SORT1
SORT6:	ROT C,1
SORT7:	HLRZ A,(P)
	POPJ P,

.FNPNT:	MOVEI A,PPA
	HRRM A,LISTF5
	MOVEI A,(PUSHJ P,)
	HRLM A,.FNPT2+1
	PUSHJ P,.+1
	MOVE A,LFRED1
	PUSHJ P,.FNPT2
	MOVE A,LFRED2
	PUSHJ P,.FNPT2
FORMF:	MOVEI CH,15
	PUSHJ P,@LISTF5
	MOVEI CH,14
	JRST @LISTF5

.FNPT2:	PUSH P,A
	PUSHJ P,PTLAB
.FN3:	MOVE A,(P)
	MOVEI B,4
	PUSHJ P,CRR1
	SOJN B,.-1
	MOVEI TT1,7
.FN239:	MOVEI D,3
.FN249:	SETZM AA
	ROTC A,6
	MOVEI T,3
.FN259:	XCT LDBT1-1(T)
	IMULI AA,10101
	SETZM C
	TRNE TT,2
	HRLM AA,C
	CAIG T,1
	JRST .FN269
	TRNE TT,1
	HRRM AA,C
.FN269:	PUSHJ P,TYPR
	IDIVI AA,10101
	SOJN T,.FN259
	JUMPE A,.FN279
	MOVEI CH,40
	REPEAT 3,PUSHJ P,PPA
	JRST .FN249
.FN279:	MOVE A,(P)
	PUSHJ P,CRR1
	SOJN D,.FN249
	SOJN TT1,.FN239
POPAJ:	POP P,A
	POPJ P,
PTLAB:	PUSHJ P,CRR1
IFE TS,[MOVE C,UFPNTR
	MOVE C,177(C)
	CAMN C,[-1]
	JRST CRR1
	LSH C,18.
	PUSHJ P,TYPR
	JRST CRR1
]
IFN TS,[PUSHJ P,AOFDIR
PTLAB2:	IOT FDRC,CH
	CAIE CH,15
	CAIN CH,12
	JRST .+3
	PUSHJ P,@LISTF5
	JRST PTLAB2
	TSCLOSE FDRC,
	PUSHJ P,LISTF4
	RSNAME C,
	PUSHJ P,TYPR
	PUSHJ P,LISTF4
	RTIME C,
	DPB C,[301400,,CTIME+1]
	LSH C,-14
	DPB C,[61400,,CTIME]
	LSH C,-14
	DPB C,[301400,,CTIME]
	MOVE C,CTIME
	PUSHJ P,TYPR
	MOVE C,CTIME+1
	PUSHJ P,SIXNTY
	PUSHJ P,LISTF4
	RDATE C,
	DPB C,[221400,,CDATE]
	PUSH P,C
	LDB CH,[220100,,(P)]
	IMULI CH,10.
	LDB C,[140400,,(P)]
	ADD C,CH
	MOVE C,MONTHS-1(C)
	PUSHJ P,SIXNTY
	MOVE C,CDATE
	PUSHJ P,TYPR
	POP P,C
	MOVEI IN,2
	JRST TYPR3

CTIME:	SIXBIT /  :  :  /
CDATE:	SIXBIT / 00,19/
MONTHS:	IRPS S,,[JAN,FEB,MARCH,APRIL
MAY,JUNE,JULY,AUG,SEPT,OCT,NOV,DEC]
	SIXBIT /S/
TERMIN
]

SPSP:	MOVEI CH,40
	JRST @LISTF5

IFN TS,[
AOFDIR:	SKIPA C,[0]
IOFDIR:	MOVEI C,6
	HRLM C,FDF
	MOVE C,UTF
	HRRM C,FDF
	TSOPEN FDRC,FDF
	POPJ P,
]

BEAST:	MOVEM B,SLN	;MITCHELL
	JUMPG B,SYML1
	MOVEI CH,PPA
	HRRM CH,LISTF5
IFE TS,[MOVEI CH,(JFCL)
	HRLM CH,.FNPT2+1
]	PUSHJ P,FRD
	PUSH P,AA
	PUSHJ P,.FNPT2
	POP P,A
	PUSHJ P,.FNPT2
	PUSHJ P,FORMF
	JRST RET

LDBT1:	REPEAT 3,LDB TT,LDBT2-1+.RPCNT*7(TT1)

LDBT2:	REPEAT 21.,[%T1==.RPCNT/7
		%T2==.RPCNT-%T1*7
		CH5.7T(AA+200+<2*%T1+5*%T2>_12.)
		]

CH5.7T:	0	;SP
DEFINE .. A,B,C,D,E,F,G,H
	IFSN H,,[PRINTX /CH5.7T LOSE!
/]
	A_31.+B_26.+C_21.+D_16.+E_11.+F_6+G_1
TERMIN

	.. 4,4,4,4,4,0,4,,	;!
	.. 12,12,12,0,0,0,0,,	;"
	.. 12,12,37,12,37,12,12,,	;#
	.. 4,17,24,16,5,36,4,,	;$
	.. 36,31,2,4,10,23,3,,	;%
	.. 4,12,4,10,25,22,15,,	;&
	.. 4,4,4,0,0,0,0,,	;'
	.. 2,4,10,10,10,4,2,,	;(
	.. 10,4,2,2,2,4,10,,	;)
	.. 0,25,16,33,16,25,0,,	;*
	.. 0,0,4,33,4,0,0,,	;+
	.. 0,0,0,0,14,4,10,,	;,
	.. 0,0,0,16,0,0,0,,	;-
	.. 0,0,0,0,0,14,14,,	;.
	.. 0,1,2,4,10,20,0,,	;/

	.. 16,21,23,25,31,21,16,,	;0
	.. 4,14,4,4,4,4,16,,	;1
	.. 16,21,1,2,4,10,37,,	;2
	.. 16,21,1,6,1,21,16,,	;3
	.. 2,6,12,37,2,2,2,,	;4 . . . OK, BEELER?
	.. 37,20,36,1,1,21,16,,	;5
	.. 16,21,20,36,21,21,16,,	;6
	.. 37,1,2,4,10,20,20,,	;7
	.. 16,21,16,21,21,21,16,,	;8
	.. 16,21,21,17,1,21,16,,	;9
	.. 0,14,14,0,14,14,0,,	;:
	.. 0,14,14,0,14,4,10,,	;;
	.. 0,2,4,10,4,2,0,,	;<
	.. 0,0,37,0,37,0,0,,	;=
	.. 0,10,4,2,4,10,0,,	;>
	.. 16,21,2,4,4,0,4,,	;?
	.. 0,16,33,25,33,16,0,,	;@
	.. 16,21,21,37,21,21,21,,	;A
	.. 36,21,21,36,21,21,36,,	;B
	.. 16,21,20,20,20,21,16,,	;C
	.. 36,21,21,21,21,21,36,,	;D
	.. 37,20,20,36,20,20,37,,	;E
	.. 37,20,20,36,20,20,20,,	;F
	.. 16,21,20,20,23,21,16,,	;G
	.. 21,21,21,37,21,21,21,,	;H
	.. 16,4,4,4,4,4,16,,	;I
	.. 7,1,1,1,1,21,16,,	;J
	.. 21,22,24,34,22,21,21,,	;K
	.. 20,20,20,20,20,20,37,,	;L
	.. 21,33,25,21,21,21,21,,	;M
	.. 21,21,31,25,23,21,21,,	;N
	.. 16,21,21,21,21,21,16,,	;O
	.. 36,21,21,36,20,20,20,,	;P
	.. 16,21,21,21,25,23,17,,	;Q
	.. 36,21,21,36,21,21,21,,	;R
	.. 16,21,20,16,1,21,16,,	;S
	.. 37,4,4,4,4,4,4,,	;T
	.. 21,21,21,21,21,21,16,,	;U
	.. 21,21,21,21,21,12,4,,	;V
	.. 21,21,21,21,21,25,12,,	;W
	.. 21,21,12,4,12,21,21,,	;X
	.. 21,21,12,4,4,4,4,,	;Y
	.. 37,2,4,16,4,10,37,,	;Z
	.. 6,4,4,4,4,4,6,,	;[
	.. 0,20,10,4,2,1,0,,	;\
	.. 14,4,4,4,4,4,14,,	;]
	.. 4,16,25,4,4,4,4,,	;^
	.. 0,4,10,37,10,4,0,,	;_

	IFN .-CH5.7T-64.,.. ,,,,,,,69


DTB:	
IFE TS,	MOVE A,TIME	;^@
IFN TS,	MOVEI AA,RTIMER	;^@
	HRROI AA,TAB	;^A
	MOVEI AA,CNTRLB	;^B
IFE TS,	MOVEI AA,CLRBF	;^C
IFN TS,	HRROI AA,TAB	;^C
	MOVEI AA,DD	;^D
IFE TS,	TLZ FF,LPTF	;^E
IFN TS,	MOVEI AA,CNTRLE	;^E
IFE TS,	MOVEI AA,LAT	;^F
IFN TS,	RDSW A,		;^F
	MOVEI AA,DECDMP	;^G - TS QUIT
	HRROI AA,TAB	;^H
	HRROI AA,TAB	;^I - TAB
	MOVEI AA,CD	;^J - LINE FEED
	HRROI AA,TAB	;^K - VERT TAB
IFE TS,	HRROI AA,TAB	;^L - FORM FEED
IFN TS,	HRROI AA,CTLL	;^L - FORM FEED
	MOVEI AA,CD	;^M - CARR RET
	MOVEI AA,FLDSZ	;^N
	MOVEI AA,SYMLST	;^O
	HRROI AA,TAB	;^P
	IFE TS,TLO FF,UREAD	;^Q
	IFN TS,MOVEI AA,CONSET	;^Q
	NTS TLZ FF,PNSTOP	;^R
IFE TS,	TLZ FF,UREAD	;^S
IFN TS,	HRROI AA,ASLEEP	;^S
	TLO FF,PNSTOP	;^T
	HRROI AA,CNTRLU	;^U
	TLZ FF,UWRITE	;^V
	TLO FF,UWRITE	;^W
	MOVE A,MARG1	;^X
	MOVE A,MARG2	;^Y
IFE TS,	HRROI AA,ADDT	;^Z
IFN TS,	TYPR2 (SIXBIT \^Z?\)	;^Z
	HRROI AA,TAB	;^[
	MOVEI AA,LIGHTS	;^\
	NTS MOVEI AA,7RI	;^]
	MOVEI AA,CNTRUP	;^^
	NTS MOVEI AA,7WI	;^_
	MOVEI AA,CD2	;
	MOVEI AA,EXCLAM	;!
	MOVEI AA,DQUOTE	;"
	MOVEI AA,COR	;#
	HRROI AA,TAB	;$
	MOVEI AA,PCNT	;%
	MOVEI AA,CAND	;&
	MOVEI AA,CD	;'
	MOVEI AA,OPEN	;(
	MOVEI AA,CLOSE	;)
	MOVEI AA,TIMES	;*
	MOVEI AA,CD2	;+
	MOVEI AA,COMMA	;,
	MOVEI AA,MINUS	;-
	JRST PNT	;.
	MOVEI AA,SLASH	;/

	JRST CDNUM	;0
	JRST CDNUM	;1
	JRST CDNUM	;2
	JRST CDNUM	;3
	JRST CDNUM	;4
	JRST CDNUM	;5
	JRST CDNUM	;6
	JRST CDNUM	;7
	JRST CDNUM	;8
	JRST CDNUM	;9
	TRO FF,COLONF	;:
	MOVEI AA,SEMICL	;;
	MOVEI AA,LSSTH	;<
	HRROI AA,PRNT	;=
	MOVEI AA,GRTH	;>
	MOVEI AA,QUESTN	;?
IFE TS,	HRROI AA,TAB	;@
IFN TS,	MOVEI AA,SYMLPT	;@
	MOVEI AA,APPEND	;A
	MOVEI A,0	;B
	MOVEI AA,CHARAC	;C
	MOVEI AA,DELETE	;D
	HRROI AA,ECMD	;E
	NTS HRROI AA,FEED	;F
	MOVEI AA,QGET	;G
	MOVEI AA,HOLE	;H
	HRROI AA,INSERT	;I
	MOVEI AA,JMP	;J
	MOVEI AA,KILL	;K
	MOVEI AA,LINE	;L
	JRST MAC	;M
	MOVEI AA,SERCHP	;N
	MOVEI AA,OG	;O
	HRROI AA,PUNCH	;P
	MOVEI AA,QREG	;Q
	MOVEI AA,REVERS	;R
	MOVEI AA,SERCH	;S
	HRROI AA,TYPE	;T
	MOVEI AA,USE	;U
	MOVEI AA,VIEW	;V
	MOVEI AA,CD	;W
	MOVEI AA,X	;X
	HRROI AA,YANK	;Y
	MOVEI AA,END1	;Z
	MOVEI AA,OPENB	;[
	MOVEI AA,BAKSL	;\
	MOVEI AA,CLOSEB	;]
	TRO FF,SLSL	;^
	MOVEI AA,LARR	;_
	HRROI AA,TAB	;`
	TYPR2 (SIXBIT \141\)	;a
REPEAT 27.,[HRROI AA,TAB
]
	JRST GO		;ALTMODE
	JRST GO		;ALTMODE
	HRROI AA,TAB	;^?
IFN .-DTB-200,[PRINTX \DTB LOSS
\]

IFN TS,[
0
UTIBUF:	BLOCK 100
UTIBE:	14_22.

UTOBUF:	BLOCK 100
UTOBE:
UTYIP:	0
UTYOP:	0
UTYOCT:	0
]
STAB:
LBF:
AC2:	BLOCK 15
BAKTAB:	BLOCK 3
	LOC STAB+40

SYMS:	BLOCK 22
VALS:	BLOCK 22
CNTS:	BLOCK 22
SYMEND:
PFL:	BLOCK LPF
GCTAB:	BLOCK GCTBL
QTAB:	BLOCK NQREG
	0
PDL:	BLOCK LPDL
IFE TS,[
PUNBO:	BLOCK 100
PUNBE:	0
RBUF:	BLOCK 250
RBEND:	0
RDCHAR=<RBEND-RBUF>*5
]
CONSTANTS
loc 36604
VARIABLES
CBUF:
IFN TS,N1KB==CBUF/2000+IFN .&1777,1
END INIT


